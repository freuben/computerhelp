<html><head><title>Playing with UGens 1</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='./../prettify.js' type='text/javascript'></script>
<script src='./../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider WORKSHOPS (extension)</div>
<div id='categories'><a href='./../Browse.html#Tutorials/ComputerDotPlay'>Tutorials/ComputerDotPlay</a></div>
<h1>Playing with UGens 1</h1>
<div id='summary'>Computer.play(Music, Creativity &amp; Code)</div>
</div>
<div class='subheader'>
<div id='related'>See also: <a href="./../Computer.html">Computer.play</a>, <a href="./../Handbook.html#Course%20Materials">Course Materials</a>, <a href="./../Handbook.html#Assessment">Assessment</a></div>
</div>
<div id='toc'>
<ul class='toc'><li class='toc1'><a href='#UGens'>UGens</a></li>
<ul class='toc'><li class='toc2'><a href='#Booting%20the%20Server'>Booting the Server</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#UGen%20arguments'>UGen arguments</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Combining%20signals'>Combining signals</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Multi-channel%20expansion%20(and%20contraction)'>Multi-channel expansion (and contraction)</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#MouseX,%20MouseY'>MouseX, MouseY</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Envelopes%20-%20Env,%20EnvGen'>Envelopes - Env, EnvGen</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Remember%20variables?'>Remember variables?</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Remember%20Global%20Variables?'>Remember Global Variables?</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Random%20sine%20waves'>Random sine waves</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Noise'>Noise</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Filters'>Filters</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Klank%20-%20bank%20of%20resonators'>Klank - bank of resonators</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Formlet'>Formlet</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Comb%20filter'>Comb filter</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Karplus-Strong'>Karplus-Strong</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Stereo%20Panning'>Stereo Panning</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Reverb'>Reverb</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Server'>Server</a></li>
<ul class='toc'><li class='toc2'><a href='#SynthDef'>SynthDef</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Karplus-Strong%20as%20SynthDef'>Karplus-Strong as SynthDef</a></li>
<ul class='toc'></ul></ul></ul></div><h2><a class='anchor' name='UGens'>UGens</a></h2>

<p>Unit Generators or UGens are objects that generate numbers very fast and run in a lower level programming language (C++) for efficiency purposes. The numbers run at a sample level and are used for synthesis/analysis of audio. To see the available UGens (unit generators) evaluate the following:<pre class='code prettyprint lang-sc'>      UGen.dumpClassSubtree</pre>

<p><strong>Excercise</strong>: Go on a tour of SuperCollider UGens:
<p><a href="./../Guides/Tour_of_UGens.html">Tour of UGens</a><div class='note'><span class='notelabel'>NOTE:</span> UGens are objects, therefore they are written starting with Upper-case letters.</div>
<p>UGens can run at audio rate (sampling rate): with the message <code class='code prettyprint lang-sc'>.ar</code>
<p>or control rate: with the message <code class='code prettyprint lang-sc'>.kr</code>
<p>Within a function, UGens can produce audio and control signals
<p>Each UGen has a help file that explains what it does.<pre class='code prettyprint lang-sc'>SinOsc //select and cmd + d</pre>
<h3><a class='anchor' name='Booting%20the%20Server'>Booting the Server</a></h3>

<p>To get any sound from a UGen, first we have to boot the server.<div class='note'><span class='notelabel'>NOTE:</span> To boot server press <em>cmd + b</em>.</div>
<p>Alternatively, you can boot the server by evaluating the following line of code:<pre class='code prettyprint lang-sc'>s.boot; //turns on the server on
s.quit; //to quit the server</pre>
<h3><a class='anchor' name='UGen%20arguments'>UGen arguments</a></h3>

<p>UGens tend to have default values for their arguments. To discover these, access the code which contains the list of arguments and their default values.<pre class='code prettyprint lang-sc'>SinOsc //select object and press cmd + i

{SinOsc.ar}.play
{SinOsc.ar(mul: 0.2)}.scope</pre>

<p><strong>Remember</strong>: to stop the sound: <em>cmd + . (period)</em><div class='note'><span class='notelabel'>NOTE:</span> UGens need the messege <code class='code prettyprint lang-sc'>.ar</code> (for audio rate) or <code class='code prettyprint lang-sc'>.kr</code> (for control rate).</div>
<p>To get help for SinOsc, select it and <em>cmd + d</em>.
<p>UGens may have one or a list of arguments (10,2,3) that are written after the message and are separated by commas<pre class='code prettyprint lang-sc'>{SinOsc.ar(mul: 0.1)}.scope
{SinOsc.ar(220,0,0.3)}.scope //args: freq, phase, mul</pre>

<p>To use different values for arguments, without specifying the value for the preceding ones, you may use the following notation:<pre class='code prettyprint lang-sc'>{SinOsc.ar(220, mul: 0.3)}.scope</pre>

<p>instead of:<pre class='code prettyprint lang-sc'>{SinOsc.ar(220, 0, 0.3)}.scope</pre>
<h3><a class='anchor' name='Combining%20signals'>Combining signals</a></h3>

<p>The argument for a UGen can be another UGen:<pre class='code prettyprint lang-sc'>{Pulse.ar(Saw.kr(MouseX.kr(0.2,100), mul: 50, add: 400/4), 0.5, mul: SinOsc.kr(1, mul:0.5, add:1))}.scope;</pre>

<p>A more simple example: here we modulate the frequency of <code class='code prettyprint lang-sc'>SinOsc</code> with another <code class='code prettyprint lang-sc'>SinOsc</code>.<pre class='code prettyprint lang-sc'>{SinOsc.ar(SinOsc.kr(0.5,0,50,400),0,0.5)}.scope; //args: freq, phase, mul, add</pre>

<p><strong>Discussion</strong>:<ul>
<li><code class='code prettyprint lang-sc'>mul</code> can be seen as the <em>range</em><li>the values for <code class='code prettyprint lang-sc'>SinOsc</code> are between -1.0 and 1.0<li>if <code class='code prettyprint lang-sc'>mul == 4</code>, then they oscillate between -4.0 and 4.0<li><code class='code prettyprint lang-sc'>add</code> can be seen as the <em>center</em><li>normal values between -1.0 and 1.0<li><code class='code prettyprint lang-sc'>add</code>, if <code class='code prettyprint lang-sc'>add == 2</code>, then values between 1.0 and 3.0<li>In the example above, the oscillator modulates between 350 Hz (400-50) and 450 Hz (400+50) every second.</ul>

<p>Other examples of modulations with <code class='code prettyprint lang-sc'>SicOsc</code>.<pre class='code prettyprint lang-sc'>{SinOsc.ar(SinOsc.kr(0.5,0,100,500),0,0.5)}.scope;
//mouse changes frequency of moduling oscillator:
{SinOsc.ar(SinOsc.kr(MouseX.kr(0.1, 10),0,100,500),0,0.5)}.scope;</pre>

<p><code class='code prettyprint lang-sc'>LFNoise0</code> produces <em>low frequency noise</em>:<pre class='code prettyprint lang-sc'>LFNoise0(freq,mul,add)   // freq is the frequency in which the values are generated

{LFNoise0.ar(10)}.scope

{Pulse.ar(LFNoise0.ar(10,100,150), mul:0.2)}.scope  // mul is the range, add is the center value
// freq between 400 and 600 Hz</pre>

<p><strong>Adding 2 signals</strong>:<pre class='code prettyprint lang-sc'>(
{ Saw.ar(400,0.3)
    +
  SinOsc.ar(120,0.3)
  }.scope
)</pre>

<p><strong>Multiplying 2 signals</strong>:<pre class='code prettyprint lang-sc'>(
{ Saw.ar(400)
    *
  Saw.ar(100)
  }.scope
)</pre>

<p>The name of parameters can be specified if it helps to understand the organisation of the expression:<pre class='code prettyprint lang-sc'>(
{Pulse.ar (freq: LFNoise0.ar( freq: MouseX.kr(0.2,1000),
                           mul: 100,
                           add: 200),
           mul: 0.3
          )
}.scope
)</pre>
<h3><a class='anchor' name='Multi-channel%20expansion%20(and%20contraction)'>Multi-channel expansion (and contraction)</a></h3>

<p>a collection (array) given to a parameter in a UGen results in a multi-channel output<pre class='code prettyprint lang-sc'>{SinOsc.ar(#[220,365])}.scope</pre>

<p>Mix is an object that mixes a multi-channel output to one channel<pre class='code prettyprint lang-sc'>{Mix.ar(SinOsc.ar((100,400..1000),0,0.6/19))}.scope</pre>

<p>The result is similar to:<pre class='code prettyprint lang-sc'>{ SinOsc.ar(220,mul: 0.3) + SinOsc.ar(365, mul: 0.3)}.scope</pre>
<h3><a class='anchor' name='MouseX,%20MouseY'>MouseX, MouseY</a></h3>

<p>Arguments may be tested using MouseX and MouseY:<pre class='code prettyprint lang-sc'>MouseX.kr(minval, maxval, warp, lag)
    //warp curve is 0 for linear, 1 for exponential or 'linear', 'exponential'
    //lag dalays the mouse movement

{SinOsc.ar(MouseX.kr(100,2000),0,0.1)}.scope
{SinOsc.ar(MouseX.kr(100,2000, 'exponential'),0,0.1)}.scope
{SinOsc.ar(MouseX.kr(100,2000, 'exponential', 5),0,0.1)}.scope //lags mouse movement for 5 secs</pre>

<p>Using MouseY too:<pre class='code prettyprint lang-sc'>(
{SinOsc.ar(freq: MouseX.kr(100,2000, 'exponential'),
    mul: MouseY.kr(0.1,0.01))  // in the upper part of the screen amp is higher
}.scope
)</pre>

<p>subsection:XLine, Line
<p>Controling values with XLine:<pre class='code prettyprint lang-sc'>    XLine (start, end, dur, mul, add, doneAction)  ar or kr
    //generates an exponential curve
    //start and end cannot be 0
    //duration in seconds</pre>

<p><div class='note'><span class='notelabel'>NOTE:</span> the last value stays unless one specifies to kill the sound with doneAction. The options for doneAction are explained in the <code class='code prettyprint lang-sc'>EnvGen</code> help file.</div>
<p><code class='code prettyprint lang-sc'>doneAction</code>: frees the synth on the Server<pre class='code prettyprint lang-sc'>{SinOsc.ar(XLine.kr(100,2000,1),0,0.3)}.scope

{SinOsc.ar(XLine.kr(100,2000,1, doneAction: 2),0,0.1)}.scope</pre>

<p>Line generates a lineal change:<pre class='code prettyprint lang-sc'>    Line (start, end, dur, mul, add, doneAction)  .ar or .kr

{SinOsc.ar(Line.kr(100,2000,10, doneAction: 2),0,0.1)}.scope</pre>
<h3><a class='anchor' name='Envelopes%20-%20Env,%20EnvGen'>Envelopes - Env, EnvGen</a></h3>

<p>Envelopes can be specified using Env<pre class='code prettyprint lang-sc'>Env.linen (1,2,1,0.6).plot
Env.linen (1,2,1,0.6).test
       linen (attackTime, sustainTime, releaseTime, level)
Env.new([0.001,1,0.3,0.001],[1,0.5,2], 'exponential').plot
Env.new([0.001,1,0.3,0.001],[1,0.5,2], 'exponential').test.plot
       Env.new([levels], [times], curve)
Env.new([0.001,1,0.3,0.001],[1,0.5,2], 'sine').test.plot
Env.new([0.001,1,0.3,0.001],[1,0.5,2], 'welch').test.plot
Env.new([0.001,1,0.3,0.001],[1,0.5,2], 4).test.plot
Env.new([0.001,1,0.3,0.001],[1,0.5,2], -4).test.plot

    Env.triangle (duration, level)
Env.triangle(1,1).test.plot

    Env.sine(duration,  level)     // Hanning window
Env.sine (1,1).test.plot

    Env.perc(attackTime, releaseTime,peakLevel, curve)
Env.perc(0.05,1,1,-4).test.plot

    cutoff(releaseTime, level, curve)   //no attack
Env.cutoff(1,1,'sine').test.plot</pre>

<p>For more envelope examples see Env help file.
<p>To generate envelopes, the <code class='code prettyprint lang-sc'>Env</code> UGen has to be used as an argument of <code class='code prettyprint lang-sc'>EnvGen</code><ul>
<li>(envelope, gate, levelScale, levelBias, timeScale, doneAction)<li>envelope - an instance of <code class='code prettyprint lang-sc'>Env</code><li>gate control signal that keeps <code class='code prettyprint lang-sc'>EnvGen</code>open</ul>

<p><pre class='code prettyprint lang-sc'>{ EnvGen.kr(Env.linen(1,2,1,0.6),doneAction: 2) * BrownNoise.ar}.scope

(
{ EnvGen.kr(Env.new([0.001,1,0.3,0.001],[1,0.5,2], 'exponential'),
    doneAction: 2)
* BrownNoise.ar}.scope
)</pre>

<p>envelope for frequency:<pre class='code prettyprint lang-sc'>(
{    Blip.ar(EnvGen.kr(Env.new([1000,500,400,200],[5,2,3],'sine'), doneAction: 2), mul: 0.1)
}.scope
)</pre>
<h3><a class='anchor' name='Remember%20variables?'>Remember variables?</a></h3>

<p>Variables can be used to break the algorithm into separate lines of code. Note that the lines of code end with a semicolon (that is how the expressions are divided.) Variables have to be inside the expression and have to be evaluated within it (if we evaluate the following piece of code line by line it produces error = we need to evaluate the whole piece of code for it to work).<pre class='code prettyprint lang-sc'>(
    var envelope, freq = 200;

    envelope = Env.new([0.001,1,0.3,0.001],[1,0.5,2], 'exponential');

    {EnvGen.ar(envelope,1,doneAction: 2) * SinOsc.ar(freq,0,0.5)}.scope;
)</pre>

<p>or<pre class='code prettyprint lang-sc'>(
{    var envelope, freq = 200;

    envelope = Env.new([0.001,1,0.3,0.001],[1,0.5,2], 'exponential');

    EnvGen.ar(envelope,1,doneAction: 2) * SinOsc.ar(freq,0,0.5)
}.scope
)</pre>
<h3><a class='anchor' name='Remember%20Global%20Variables?'>Remember Global Variables?</a></h3>

<p>Variables can be local or global. Variables that are local and have to be declared. Global variables can be declared and accessed wherever and whenever, regardless where they are in a piece of code or when they are declared. Global variables can be letters a-z (stored in the interpreter), but may also be written using <code class='code prettyprint lang-sc'>~</code> before a written variable.<pre class='code prettyprint lang-sc'>Example:

~cuatro = 4;
~cinco = 5;

~cuatro + ~cinco;</pre>

<p>These type of global variables are stored in an Environment. If you want to clear the enviroment you can by evaluating the following piece of code:<pre class='code prettyprint lang-sc'>currentEnvironment.clear;

~cuatro
~cinco</pre>

<p>For more information about environments select and <em>cmd + d</em><pre class='code prettyprint lang-sc'>Environment</pre>
<h3><a class='anchor' name='Random%20sine%20waves'>Random sine waves</a></h3>

<p>Fill an array for random frequencies:<pre class='code prettyprint lang-sc'>Array.fill(10,1.0.rand)  // fills an array with the same random value
Array.fill(10,{1.0.rand})  // fills an array with different values</pre>

<p>Another way of doing the same:<pre class='code prettyprint lang-sc'>{1.0.rand}.dup(10)    // dup duplicates an array produced by a function several times
{1.00.rand} ! 10      // ! same as dup (ver SymbolicNotations)

[1,0.5,0.3].normalizeSum //normalizes an array:

(
{
var harmArray, ampArray, fund,num; //variable declaration
fund = 100;
num = 20;
harmArray ={rrand(1.0, 20)} ! num;  // an array of num amount of values
ampArray = {1.0.rand}.dup(num).normalizeSum; //normalize the array so that amp is not too much
Mix.ar(SinOsc.ar(harmArray * fund, 0, ampArray));
}.scope
)</pre>

<p>We add an envelope to the random frequencies:<pre class='code prettyprint lang-sc'>(
{
var harmArray, ampArray, fund,n,env;
fund = 100;
n = 20;
harmArray ={rrand(1,20.0)} ! n;
ampArray = {1.0.rand}.dup(n).normalizeSum;
env = Env.linen(1,10,3,0.6); //Lineal envelope
Mix.ar(SinOsc.ar(harmArray * fund, 0, ampArray) * EnvGen.kr(env,1,doneAction:2));
}.scope;
)</pre>

<p>a different envelope for each frequency:<pre class='code prettyprint lang-sc'>(
{
var harmArray, ampArray, fund,n,env; fund = 100;
n = 20;
harmArray ={rrand(1.0,20)}! n;
ampArray = {1.0.rand}.dup(n).normalizeSum;
env = {EnvGen.kr(Env.linen(rrand(1,10.0).postln,10,3,0.6))} ! n;
Mix.ar(SinOsc.ar(harmArray * fund, 0, ampArray) * env);
}.scope;
)</pre>

<p>Another way duplicate the whole function:<pre class='code prettyprint lang-sc'>(
{
var harmArray, ampArray, fund,n; fund = 100;
n = 20;
Mix.ar({SinOsc.ar(rrand(1,20.0) * fund, 0, 0.3.rand) *
    EnvGen.kr(Env.linen(rrand(1.0,10),rrand(5.0,10),3,0.2))} ! n);
}.scope;
)</pre>
<h3><a class='anchor' name='Noise'>Noise</a></h3>
<pre class='code prettyprint lang-sc'>WhiteNoise.dumpClassSubtree //different types of Noise

{WhiteNoise.ar(0.5)}.scope
{PinkNoise.ar(0.5)}.scope
{BrownNoise.ar(0.5)}.scope
{GrayNoise.ar(0.5)}.scope

Impulse.ar(freq, phase, mul, add) // Random impulses
{Impulse.ar(MouseX.kr(1,500))}.scope

Dust.ar(density, mul, add) // random impulses
{Dust.ar(MouseX.kr(10,500))}.scope</pre>

<p><code class='code prettyprint lang-sc'>Dust</code> as trigger of an envelope. The argument gate activates when the signal is &gt; (greater than) 0 :<pre class='code prettyprint lang-sc'>(
{    SinOsc.ar(440, mul: EnvGen.kr(Env.perc(0.001,1.0),
                              Dust.kr(MouseX.kr(0.5,5))))
}.scope
)

Env.perc(0.001,0.05).plot</pre>
<h3><a class='anchor' name='Filters'>Filters</a></h3>

<p><code class='code prettyprint lang-sc'>Filter.dumpClassSubtree</code>
<p>RLPF: resonant low pass filter RHPF: resonant high pass filter BPF: bandpass filter<pre class='code prettyprint lang-sc'>RLPF.ar(in, freq, rq, mul, add)</pre>

<p>Freq is the cuttoffFreq, rq is the reciprocal of Q: bandwidth/cutoffFreq<pre class='code prettyprint lang-sc'>{ RLPF.ar(WhiteNoise.ar,500,0.1)}.scope
{ RLPF.ar(WhiteNoise.ar,500,0.9)}.scope
{ RLPF.ar(WhiteNoise.ar,MouseX.kr(200,2000,0, 0.5),MouseY.kr(0.01,0.5,0, 0.5))}.scope
// MouseX is cutoff freq and MouseY is rq</pre>

<p>BPF - 2nd order Butterworth bandpass filter<pre class='code prettyprint lang-sc'>{ BPF.ar(WhiteNoise.ar,MouseX.kr(200,2000,0, 0.5),MouseY.kr(0.01,0.5,0, 0.5))}.scope</pre>

<p><code class='code prettyprint lang-sc'>Resonz</code> - resonant filter<pre class='code prettyprint lang-sc'>Resonz.ar(in,freq,rq, mul,add)

{Resonz.ar(WhiteNoise.ar(0.5),2000,0.1)}.scope

// modulate bandwidth
{ Resonz.ar(WhiteNoise.ar(0.5), 2000, XLine.kr(1, 0.001, 8)) }.play

(
  {Resonz.ar(WhiteNoise.ar(0.3),2000,0.1)
  +
  Resonz.ar(WhiteNoise.ar(0.3),1207,0.1)
  +
 Resonz.ar(WhiteNoise.ar(0.3),387,0.1)}.scope;
)</pre>
<h3><a class='anchor' name='Klank%20-%20bank%20of%20resonators'>Klank - bank of resonators</a></h3>
<pre class='code prettyprint lang-sc'>Klank.ar(specificationsArrayRef, input, freqscale, freqoffset, decayscale)</pre>

<p>specificationsArrayRef - a Ref to an Array of three Arrays:<ul>
<li>frequencies - an Array of filter frequencies.<li>amplitudes - an Array of filter amplitudes, or nil. If nil, then amplitudes default to 1.0<li>ring times - an Array of 60 dB decay times for the filters.</ul>

<p>If there are only two arrays in the main array, the ring times have been omitted
<p>Use <code class='code prettyprint lang-sc'>`</code> to prevent multi-channel expantion<pre class='code prettyprint lang-sc'>(
{
    Klank.ar(
        `[[100,200,300,400,500,600,700,800, 900,1000],
          [0.05,0.2,0.04,0.06,0.11,0.01,0.15,0.03,0.15,0.02]],
        PinkNoise.ar(MouseX.kr(0.01,0.1)))
}.scope
)


Array.series(10,50,100)     // size, start, step
Array.series(10,1.0,-0.1)
(1.0,0.9..0.1)

(
{
Klank.ar(
    `[Array.series(10,50,134),
      Array.series(10,1.0,-0.05)],
    PinkNoise.ar(0.01))
}.scope
)</pre>

<p>To convert dBs (decibels) to amp values:<pre class='code prettyprint lang-sc'>        -6.dbamp</pre>

<p>Using amps as dBs (freq and amps from formant table of Csound for alto E)<pre class='code prettyprint lang-sc'>(
{
Klank.ar(
    `[[400,1600,2700,3300,4950],
      [0,-24,-30,-35,-60].dbamp.postln],
      PinkNoise.ar(0.01))
}.scope
)</pre>

<p>Freq and amps from formant table of Csound for alto O<pre class='code prettyprint lang-sc'>(
{
Klank.ar(
    `[[450,800,2830,3500,4950],
      [0,-9,-16,-28,-55].dbamp],
      PinkNoise.ar(0.01))
}.scope
)</pre>

<p><div class='note'><span class='notelabel'>NOTE:</span> look also at DynKlank (it is a dynamic Klank values of arrays can change)</div><h3><a class='anchor' name='Formlet'>Formlet</a></h3>

<p>FOF-like filter<pre class='code prettyprint lang-sc'>Formlet.ar(in, freq, attacktime, decaytime, mul, add)</pre>

<p>Resonant filter with an impulse response similar to that of a sine wave with exponential decay.
<p>If the attacktime and decaytime are the same, the signal is canceled. if attacktime &gt; decaytime, the impulse response is inverted.<pre class='code prettyprint lang-sc'>{Formlet.ar(PinkNoise.ar(0.1), 1000,0.01,0.1)}.scope
{Formlet.ar(Impulse.ar(20,0.5), 1000,0.01,0.1)}.scope  // Impulse maybe sounds</pre>

<p>Band-limited impulse oscillator:<pre class='code prettyprint lang-sc'>Blip.ar(kfreq, numharmonics, mul, add)

    {Blip.ar(100,100,0.5)}.scope

    {Blip.ar(SinOsc.kr(5,0,20,300), 1000,0.4)}.scope</pre>

<p>Modulate formant frequency with Blip input signal:<pre class='code prettyprint lang-sc'>(
{
    var in;
    in = Blip.ar(SinOsc.kr(5,0,20,300), 1000, 0.1);
    Formlet.ar(in, XLine.kr(1500,700,8, doneAction: 2), 0.005, 0.04);
}.scope;
)</pre>
<h3><a class='anchor' name='Comb%20filter'>Comb filter</a></h3>
<pre class='code prettyprint lang-sc'>CombL.ar(in,maxdelaytime, delaytime, decaytime, mul, add);</pre>

<p><code class='code prettyprint lang-sc'>CombL</code> uses lineal delay (see also CombN, CombC)
<p>decayTime is the time that it takes for the echoes to diminish by 60 dB<pre class='code prettyprint lang-sc'>{ CombL.ar(WhiteNoise.ar(0.1), 0.01, 0.01, 0.2) }.scope;

// modulate delayTime:
{ CombL.ar(WhiteNoise.ar(0.1), 0.01, XLine.kr(0.001,0.01,10), 0.2) }.scope;</pre>
<h3><a class='anchor' name='Karplus-Strong'>Karplus-Strong</a></h3>

<p>Using CombL for Karplus-Strong model:<pre class='code prettyprint lang-sc'>69.midicps                    // frequency
69.midicps.reciprocal          // period

(
{
    var burstEnv, att = 0, dec = 0.001;
    var delayTime, decayTime = 10;
    var midiPitch = 69;
    delayTime = midiPitch.midicps.reciprocal;
    burstEnv = EnvGen.kr(Env.perc(att, dec));
    CombL.ar(WhiteNoise.ar(burstEnv), delayTime, delayTime, decayTime);
}.scope
)</pre>

<p>Using Mouse as Trigger<pre class='code prettyprint lang-sc'>(
{
    var burstEnv, att = 0, dec = 0.05;  // now with more dec
    var delayTime, decayTime = 10;
    delayTime = MouseX.kr(36,74, lag: 0).midicps.reciprocal;  // MouseX controls note
    burstEnv = EnvGen.kr(Env.perc(att, dec), gate: MouseButton.kr(0,1,0)); // MouseButton controls gate
    CombL.ar(WhiteNoise.ar(burstEnv), delayTime, delayTime, decayTime);
}.scope
)</pre>

<p>Try with different Noises (for example: <code class='code prettyprint lang-sc'>PinkNoise, BrownNoise, GrayNoise</code>, etc)<h3><a class='anchor' name='Stereo%20Panning'>Stereo Panning</a></h3>

<p><code class='code prettyprint lang-sc'>Pan2.ar(signal, position)</code> -1 is left, 1 is right<pre class='code prettyprint lang-sc'>{Pan2.ar(Dust.ar(50), MouseX.kr(-1,1))}.scope</pre>

<p>Adding panning to karplus<pre class='code prettyprint lang-sc'>(
{
    var burstEnv, att = 0, dec = 0.05;
    var delayTime, decayTime = 10;
    delayTime = MouseX.kr(36,74, lag: 0).midicps.reciprocal;
    burstEnv = EnvGen.kr(Env.perc(att, dec), gate: MouseButton.kr(0,1,0));
    Pan2.ar(CombL.ar(WhiteNoise.ar(burstEnv), delayTime, delayTime, decayTime),
            MouseX.kr(1,-1))
}.scope
)</pre>

<p><h3><a class='anchor' name='Reverb'>Reverb</a></h3>

<p>Add reverb McCartney style to karplus an array of <code class='code prettyprint lang-sc'>AllPass</code> filter, without interpolation<pre class='code prettyprint lang-sc'>y = signal;
Mix.fill(5,{ y = AllpassN.ar(y, 0.05,0.05.rand,3)});
signal + (y * 0.0) // dry signal + signal with reverb

(
{
    var burstEnv, att = 0.0, dec = 0.05, y, panned;
    var delayTime, decayTime = 10;
    delayTime = MouseX.kr(36,74, lag: 0).midicps.reciprocal;
    burstEnv = EnvGen.kr(Env.perc(att, dec), gate: MouseButton.kr(0,1,0));
    panned = Pan2.ar(CombL.ar(WhiteNoise.ar(burstEnv), delayTime, delayTime, decayTime),
            MouseX.kr(1,-1));
    y = panned;
    Mix.fill(5, { y = AllpassN.ar(y, 0.05,0.05.rand, 1)});
    panned + (y * 0.4)
}.scope
)</pre>
<div class='note'><span class='notelabel'>NOTE:</span> For better quality reverb look at: FreeVerb and FreeVerb2</div><h2><a class='anchor' name='Server'>Server</a></h2>

<p>Synthesis engine that receives commands (OSC) from the language (or some other application)<ul>
<li>The synth server and its clients may be on the same machine or across a network<li>Synthesis definitions (<code class='code prettyprint lang-sc'>SynthDef</code>) are loaded to the server<li>The server does not send or receive MIDI. It is expected that the client will send control messages.<li>Server uses trees of nodes (a network) which are either synths or groups<li>Synth is a collection of Unit Generators that run together<li>Group is a collection of nodes represented as a linked list<li>(each node points to the next one).<li>A new node is added to the head (the beginning) or the tail (the end) of the list.<li>The order of execution of SynthDefs can be important (e.g. a signal must first be made before it can be processed in a reverb unit).<li>The order of execution is determined with the order of the nodes (head or tail).<li>Audio can be routed from one SynthDef to another and to the Audio Output device via Audio Buffers (an array of 128 channels).</ul>

<p>Control information can be routed with control buses (an array of 4096 buses)<pre class='code prettyprint lang-sc'>{SinOsc.ar(440,mul: 0.1)}.play(s);</pre>

<p>the messages play and scope produce a synthesis definition (look at the post window):<pre class='code prettyprint lang-sc'>Synth("temp__46" : 1005)   // the name (in this case temporal name itgoing to be different than the one here) and the node of the synth definiton will appear in the post window)</pre>

<p>Global variables (for example letters a z) can be used to store the synthesis definition<pre class='code prettyprint lang-sc'>a = {SinOsc.ar(440,mul: 0.1)}.play(s);
b = {SinOsc.ar(660,mul: 0.1)}.play(s);
c = {SinOsc.ar(740,mul: 0.1)}.play(s);

a.free;
b.free;
c.free;</pre>
<h3><a class='anchor' name='SynthDef'>SynthDef</a></h3>

<p>A SynthDef gives a name to a specific patch. When loaded to the server, a SynthDef can receive messages to change the values thorough its arguments.
<p>Basic format:<pre class='code prettyprint lang-sc'>SynthDef("name", { Out.ar(busNumber,UGen});</pre>

<p><code class='code prettyprint lang-sc'>"name"</code> can also be written as <code class='code prettyprint lang-sc'>\name</code><pre class='code prettyprint lang-sc'>(
SynthDef("sines",{ |freq = 440,bus = 0|
    Out.ar(bus,SinOsc.ar(freq,0,0.1));
    }).load(s);   // save definition and loaded to server (s)
)</pre>

<p>SynthDefs can be written to a file for later use, or written and loaded, or just loaded.
<p>The default directory for SynthDef files is synthdefs/
<p>(look at /Library/Application Support/SuperCollider/synthdefs/)<ul>
<li>writeDefFile (dir) writes the def as a file<li>load(server) write the defFile and send a message to server to load the file<li>send(server) compile the def and send it to server without writing it to a file<li>play(server) compile def, sends to server, and creates one synth from this definition</ul>

<p>Read help file of <code class='code prettyprint lang-sc'>SynthDef</code> to see more messages the class responds to.
<p>It is possible to send raw messages (OSC messages) directly to the server to make a new instance of a SynthDef or to change the argument value of an existing one.
<p>A description of the OSC messages available:<pre class='code prettyprint lang-sc'> [Server-Command-Reference]</pre>

<p>A convenient way of creating instances of a synth is using the classes from the SC language<pre class='code prettyprint lang-sc'>Synth("sines")
Synth("sines",[\freq, 200])


z = Synth("sines");

z.run(false);
z.run(true);

z.set("freq",600);
z.set("bus", 1);
z.set("freq",300, "bus",0);</pre>

<p>To free the synth from the Server:<pre class='code prettyprint lang-sc'>z.free;</pre>

<p>Another SynthDef:<pre class='code prettyprint lang-sc'>(
SynthDef("filterNoise", {arg center = 2000;
    Out.ar(0,Resonz.ar(WhiteNoise.ar(0.5), center,0.1));
    }).load(s);
)

a = Synth("filterNoise",[\center,500]);
b = Synth("filteredNoise",[\center,1000]);
a.free;
b.free;</pre>

<p>To browse available SynthDefs:<pre class='code prettyprint lang-sc'>(
SynthDescLib.global.read;
SynthDescLib.global.browse;
)</pre>
<h3><a class='anchor' name='Karplus-Strong%20as%20SynthDef'>Karplus-Strong as SynthDef</a></h3>
<pre class='code prettyprint lang-sc'>(
SynthDef("Karplus",
{
    arg midiPitch = 69, decayTime = 2.0;
    var burstEnv, att = 0, dec = 0.001, signalOut, delayTime;
    delayTime = midiPitch.midicps.reciprocal;
    burstEnv = EnvGen.kr(Env.perc(att, dec));
    signalOut = CombL.ar(WhiteNoise.ar(burstEnv), delayTime, delayTime, decayTime);
    DetectSilence.ar(signalOut, doneAction: 2);
    Out.ar(0,signalOut);
}).send(s)
)</pre>

<p>From this example:<ul>
<li>Note the different syntax in the argument declaration (<code class='code prettyprint lang-sc'>arg ...</code> ; instead of <code class='code prettyprint lang-sc'>|arg, arg|</code>)<li>Note the way <code class='code prettyprint lang-sc'>Out</code> is used<li>Note that we are just sending this <code class='code prettyprint lang-sc'>SynthDef</code> to the server and we are not writing it as a file<li><code class='code prettyprint lang-sc'>DetectSilence</code> is a <code class='code prettyprint lang-sc'>UGen</code> that sends a <code class='code prettyprint lang-sc'>doneAction</code> the input is smaller than a threshold (to detect silence)</ul>
<pre class='code prettyprint lang-sc'>Synth("Karplus")
Synth("Karplus", [\midiPitch, 60])   // uses a different node every time

a = Synth("Karplus"); //the synth turns off because we use DetectSilence
a.set(\midiPitch, 60);   // if we want to change the value, we have to do it very fast, before DetectSilence turns the synth off

Synth("Karplus", [\midiPitch, rrand(50,80), \decayTime, 3.0]); //evaluate several times!</pre>
<div class='note'><span class='notelabel'>NOTE:</span> Various examples, ideas, etc. have been taken or derived from the SC documentation, Paul Berg, David Cottle, Nick Collins, and probably lots of other people in the SuperCollider community.</div><div class='doclink'>helpfile source: <a href='file:///Users/federicoreuben/Library/Application Support/SuperCollider/Extensions/ComputerPlay/HelpSource/Workshops/Playing with UGens 1.schelp'>/Users/federicoreuben/Library/Application Support/SuperCollider/Extensions/ComputerPlay/HelpSource/Workshops/Playing with UGens 1.schelp</a><br>link::Workshops/Playing with UGens 1::<br>sc version: 3.8.0</div></div></body></html>