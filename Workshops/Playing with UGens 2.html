<html><head><title>Playing with UGens 2</title>
<link rel='stylesheet' href='./../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../scdoc.js' type='text/javascript'></script>
<script src='./../docmap.js' type='text/javascript'></script>
<script src='./../prettify.js' type='text/javascript'></script>
<script src='./../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider WORKSHOPS (extension)</div>
<div id='categories'><a href='./../Browse.html#Tutorials/ComputerDotPlay'>Tutorials/ComputerDotPlay</a></div>
<h1>Playing with UGens 2</h1>
<div id='summary'>Computer.play(Music, Creativity &amp; Code)</div>
</div>
<div class='subheader'>
<div id='related'>See also: <a href="./../Computer.html">Computer.play</a>, <a href="./../Workshops/Playing with UGens 1.html">Playing with UGens 1</a></div>
</div>
<div id='toc'>
<ul class='toc'><li class='toc1'><a href='#Playing%20with%20UGens%202'>Playing with UGens 2</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Buffers'>Buffers</a></li>
<ul class='toc'><li class='toc2'><a href='#PlayBuf'>PlayBuf</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Buffer%20class'>Buffer class</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#DiskIn'>DiskIn</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Delays'>Delays</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Quick%20Intro%20to%20Routines'>Quick Intro to Routines</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Additive%20Synthesis'>Additive Synthesis</a></li>
<ul class='toc'><li class='toc2'><a href='#Envelopes:'>Envelopes:</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Wavetable%20synthesis'>Wavetable synthesis</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Subtractive%20Synthesis'>Subtractive Synthesis</a></li>
<ul class='toc'><li class='toc2'><a href='#Filters:'>Filters:</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Noise'>Noise</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Multiple%20Wavetable%20Synthesis'>Multiple Wavetable Synthesis</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Modulation%20Synthesis'>Modulation Synthesis</a></li>
<ul class='toc'><li class='toc2'><a href='#Ring%20modulation'>Ring modulation</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Amplitude%20modulation:'>Amplitude modulation:</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Frequency%20modulation'>Frequency modulation</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#Phase%20modulation'>Phase modulation</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Dynamic%20waveshaping'>Dynamic waveshaping</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#WaveTerrain%20Synthesis.'>WaveTerrain Synthesis.</a></li>
<ul class='toc'></ul></ul></div><h2><a class='anchor' name='Playing%20with%20UGens%202'>Playing with UGens 2</a></h2>

<p>Fist, let's boot the server:<pre class='code prettyprint lang-sc'>s.boot;</pre>

<p>Now let's kill it, just for fun:<pre class='code prettyprint lang-sc'>s.free;</pre>

<p>You can us the shortcut <em>cmd + b</em> to boot the Server, It's easier and faster.<div class='note'><span class='notelabel'>NOTE:</span> Remember, you can boot the server with <em>cmd + b</em></div><h2><a class='anchor' name='Buffers'>Buffers</a></h2>
<ul>
<li><code class='code prettyprint lang-sc'>Buffers</code> are arrays of 32-bit floating point values.<li><code class='code prettyprint lang-sc'>Buffers</code> are on the server so that they can be used by synths.<li><code class='code prettyprint lang-sc'>Buffers</code> need to be allocated, filled, and at some point freed.<li><code class='code prettyprint lang-sc'>Buffers</code> can be allocated, loaded and freed while synthesis is running.<li><code class='code prettyprint lang-sc'>Buffers</code> can be used for wave tables, sample buffers, delay lines, ...</ul>

<p>The number of buffers on a server is set at boot time.<pre class='code prettyprint lang-sc'>o = ServerOptions.new;
o.numBuffers;</pre>
<h3><a class='anchor' name='PlayBuf'>PlayBuf</a></h3>

<p><code class='code prettyprint lang-sc'>PlayBuf</code> is a UGen to listen/manipulate sound files.
<p><code class='code prettyprint lang-sc'>PlayBuf</code> - sample playback oscillator (numChannels, bufnum, rate, trigger, startPos, loop)<ul>
<li><em>rate</em>: 1.0 is the server's sample rate, 2.0 is one octave up, 0.5 is one octave down<li><em>loop</em> = 1 is true, 0 is false</ul>
<h3><a class='anchor' name='Buffer%20class'>Buffer class</a></h3>

<p>Buffers can also be accessed with the <code class='code prettyprint lang-sc'>Buffer</code> class (which may be more convenient than the messaging style)
<p><code class='code prettyprint lang-sc'>Buffer.read</code> allocates a buffer and reads a sound file into it.<pre class='code prettyprint lang-sc'>b = Buffer.read(s, "/Users/freuben/Music/Stelarc/insects/apis.genista.wav");

{ PlayBuf.ar(1, b.bufnum, 1, loop: 1) }.scope</pre>

<p>Mouse control of rate (transposition)<pre class='code prettyprint lang-sc'>{ PlayBuf.ar(1, b.bufnum, MouseX.kr(0.5,2), loop: 1) }.scope

b.bufnum   // returns buffer number
b.numChannels   // returns the number of channels in the buffer

b.free  // return the memory and free buffer ID, IMPORTANT</pre>

<p><code class='code prettyprint lang-sc'>BufRateScale</code>
<p>File being read into a buffer may have a different sample than SC is using
<p><code class='code prettyprint lang-sc'>BufRateScale.kr(bufnum)</code> returns a ratio to adjust the playback of the soundfile<pre class='code prettyprint lang-sc'>b = Buffer.read(s, "sounds/a11wlk01.wav");  // sample rate of this file is 11025</pre>

<p>to scale the playback:<pre class='code prettyprint lang-sc'>{ PlayBuf.ar(1, b.bufnum, BufRateScale.kr(b.bufnum), loop: 1) }.scope

b.free</pre>

<p><code class='code prettyprint lang-sc'>BufFrames</code>
<p><code class='code prettyprint lang-sc'>BufFrames.kr(bufnum)</code> returns the current number of allocated frames
<p>it can be used to specify starting halfway through the buffer or some place else<pre class='code prettyprint lang-sc'>b = Buffer.read(s, "sounds/a11wlk01-44_1.aiff");
(
var start = 1/2;
{ PlayBuf.ar(1, b.bufnum, BufRateScale.kr(b.bufnum),
    startPos: BufFrames.kr(b.bufnum) * start, loop: 1)
}.scope
)</pre>

<p>note that loop returns to the beginning in the above example
<p>a trigger (&lt;= 0 to &gt; 0) causes the playback to start from startPos
<p>using a trigger without looping
<p>Impulse.kr produces a trigger with a certain frequency<pre class='code prettyprint lang-sc'>(
    var start = 1/2;
    { PlayBuf.ar(1, b.bufnum, BufRateScale.kr(b.bufnum),
        Impulse.kr(1/3),
        startPos: BufFrames.kr(b.bufnum) * start, loop: 0)
    }.scope
)</pre>

<p>Using a trigger with looping:
<p>the trigger should occur with a frequency that is
<p>(serverSampleRate / numberOfFrames) * rateScale
<p>Server's sample rate:<pre class='code prettyprint lang-sc'>s.sampleRate</pre>
<pre class='code prettyprint lang-sc'>(
var start = 1/2;
{ PlayBuf.ar(1, b.bufnum, BufRateScale.kr(b.bufnum),
    Impulse.kr(s.sampleRate / (BufFrames.kr(b.bufnum) * start) *
        BufRateScale.kr(b.bufnum)),
    startPos: BufFrames.kr(b.bufnum) * start, loop: 1)
}.scope
)

b.free</pre>
<h3><a class='anchor' name='DiskIn'>DiskIn</a></h3>

<p>To continuously play longer sound file from disk. One buffer must be preloaded.
<p>using <code class='code prettyprint lang-sc'>Buffer object</code>
<p>(server, path, startFrame, numChannels)<pre class='code prettyprint lang-sc'>b = Buffer.cueSoundFile(s, "/Users/freuben/Music/Aleks/CONCERTRECORDING/Improvisations/StringQ_1_MONO.aif", 0, 1); //or your own path

x = { DiskIn.ar(1, b.bufnum) }.play;

b.close;    // close file</pre>

<p>Again, note the like named instance method, but different arguments<pre class='code prettyprint lang-sc'>b.cueSoundFile("/Users/freuben/Music/Aleks/CONCERTRECORDING/Improvisations/StringQ_1_MONO.aif", 0);

x.free; b.close; b.free;    // close and free</pre>
<h2><a class='anchor' name='Delays'>Delays</a></h2>

<p>Phase demonstration:<pre class='code prettyprint lang-sc'>~freq = 440;
{[SinOsc.ar(~freq), SinOsc.ar(~freq,MouseX.kr(0,2pi))]*0.4}.scope;

~freq = 440;
{Mix.ar(SinOsc.ar(~freq) + SinOsc.ar(~freq,MouseX.kr(0,2pi)))*0.4}.scope;

~freq = 220;
{Mix.ar(Pulse.ar(~freq) + SinOsc.ar(~freq,MouseX.kr(0,2pi)))*0.4}.scope;</pre>

<p>Playing with delay change (pitch shifting!):<pre class='code prettyprint lang-sc'>b = Buffer.loadDialog;

(
{var input, delay;
    input = PlayBuf.ar(2,b.bufnum, startPos:20*44100);
    delay = DelayL.ar(input, 10, MouseX.kr(0.1,10.0).lag(3));
    input + delay;
}.play;
)

(
{var input, delay, mod;
    input = PlayBuf.ar(2,b.bufnum, startPos:20*44100);
    //    mod = SinOsc.ar(MouseX.kr(0.1,1).lag(3));
    //mod =LFNoise1.ar(MouseX.kr(0.1,3).lag(3));
    mod = PlayBuf.ar(2,b.bufnum, MouseX.kr(0.0,0.05).lag(3), startPos:20*44100);
    delay = DelayL.ar(input, 10, mod.linlin(-1,1,0.0,MouseY.kr(0,10).lag(3)));
    //delay;
    input + delay / 2;
}.play;
)</pre>

<p>Constructive and destructive interference:<pre class='code prettyprint lang-sc'>(
{var input, delay, mod;
    input = SinOsc.ar(440,0,0.5);
    mod = MouseX.kr(0.5,1.0);
    delay = DelayL.ar(input, 1/440*mod, 1/440*mod);
    input + delay;
}.scope
)</pre>

<p>Comb effect (hear the comb across the spectrum?):<pre class='code prettyprint lang-sc'>(
{var input, delay;
    input = SinOsc.ar(MouseX.kr(20,10000),0,0.5);
    delay = DelayL.ar(input, 1/1000, 1/1000);
    input + delay;
}.scope
)</pre>

<p>Delays in synthesis - simple physical model using comb filter:<pre class='code prettyprint lang-sc'>(
{var input, delay;
    input = PinkNoise.ar*EnvGen.kr(Env.perc(0.01,0.2));
    [input, input]
}.play //centre
)</pre>

<p>Delay and sound localisation:<pre class='code prettyprint lang-sc'>(
{var input, delay;
    input = PinkNoise.ar*EnvGen.kr(Env.perc(0.01,0.2));
    delay = DelayL.ar(input, 0.01, 0.01)*0.5;
    [input, delay];
}.play //left
)</pre>
<h2><a class='anchor' name='Quick%20Intro%20to%20Routines'>Quick Intro to Routines</a></h2>

<p>In SC to schedule events, we use Routines.
<p><code class='code prettyprint lang-sc'>Routine</code> is a series of events, values or messages that are executed in a sequence. It’s a function that can be stopped, reset and restarted ones it is started.
<p>The yield or wait (wait just accepts simple numbers) messages waits a specitic amount of time.
<p>The simplest way to define a <code class='code prettyprint lang-sc'>Routine</code> is with the message <code class='code prettyprint lang-sc'>.fork</code> at the end of a function:<pre class='code prettyprint lang-sc'>(
{"hi".postln;
1.wait;
"bye".postln;
}.fork
)</pre>

<p>We can specify how many times we want to repeat the routine with another function within the routine:<pre class='code prettyprint lang-sc'>(
{
    3.do{
    "hi".postln;
0.2.wait;
        "bye".postln;
0.3.wait;
    }
}.fork
)</pre>

<p>We can loop a routine and assign it to a global variable, to stop and restart<pre class='code prettyprint lang-sc'>(
a = {
    {
    "hi".postln;
0.2.wait;
        "bye".postln;
0.3.wait;
    }.loop
}.fork
)

a.stop; //stop
a.reset.play; //reset and play again</pre>
<div class='note'><span class='notelabel'>NOTE:</span> We will come back to Routines and Scheduling later on.</div><h2><a class='anchor' name='Additive%20Synthesis'>Additive Synthesis</a></h2>
<h3><a class='anchor' name='Envelopes:'>Envelopes:</a></h3>
<pre class='code prettyprint lang-sc'>Env.new([0,1,0], [1,9]).plot; //values, time</pre>

<p>Another more complex curved envelope:<pre class='code prettyprint lang-sc'>a = Array.fill(29, {rrand(0.0,1)});
d = Array.fill(29, {rrand(0.1,0.5)}).sort;
a = a ++ [0];
c = Env.new(a,d, curve:'sin');
c.plot;

{SinOsc.ar(440)*EnvGen.kr(c, doneAction: 2);}.play;</pre>

<p>Now use envelope to control frequency:<pre class='code prettyprint lang-sc'>{SinOsc.ar(EnvGen.kr(c, doneAction: 2, levelScale: 200, levelBias: 300))*EnvGen.kr(c, doneAction: 2);}.play;</pre>
<h3><a class='anchor' name='Wavetable%20synthesis'>Wavetable synthesis</a></h3>

<p><code class='code prettyprint lang-sc'>Osc</code> is a table-lookup oscillator.
<p>Change the buffer while its playing:<pre class='code prettyprint lang-sc'>b = Buffer.alloc(s, 4096, 1);
b.sine1(1.0/[1,2,3,4,5,6], true, true, true);

{Osc.ar(b.bufnum, [80,80.2], 0, 0.2)}.play;

b.sine1(Array.rand(32,0,1).cubed, true, true, true); //change wavetable

{Osc.ar(b.bufnum, MouseX.kr(100,1000), 0, 0.2)}.play;

{Osc.ar(b.bufnum, [80,80.2], 0, 0.2)}.play;

{50.do{b.sine1(Array.rand(32,0,1).cubed, true, true, true);
    0.25.wait;
}}.fork;

(
{Osc.ar(b.bufnum, EnvGen.kr(c, doneAction: 2, levelScale: 200, levelBias: 300))*EnvGen.kr(c, doneAction: 2);}.play;
{
    d.do{|item, index|
        item.wait;
        b.sine1(Array.rand((100*a[index]).round.postln,0,1).cubed, true, true, true);
}}.fork;
)

{Osc.ar(b.bufnum, [80,80.2], 0, 0.2)}.play;

b.sine1(1.0/[1,2,3,4,5,6], true, true, true);

b.sine1(1.0/(1..100), true, true, true);

b.sine1(Array.fill(50,{[0,1].choose}), true, true, true); //evaluate serveral times

b.sine1([1,1,1,0,0,0,1,1,1,0,0,0,0,0,0.5,0.5,0.5], true, true, true);</pre>

<p><strong>Fixed waveform additive synthesis</strong><pre class='code prettyprint lang-sc'>(
SynthDef(\addSynth, {arg freq=40, amp=0.5, lag=30; var signal;
    signal = SinOsc.ar(freq.lag(lag),0,amp.lag(lag));
    Out.ar([0,1],signal)
}).send(s);
)

(
~arr = Array.fill(12, {rrand(200,10000)}).sort;
~synthArr = [];
~arr.do{|item|
    ~synthArr = ~synthArr.add(Synth(\addSynth, [\freq, item, \amp, 0.5/~arr.size]));
}
)

~arr = [~arr[0]] ++ Array.fill(~arr.size-1,{rrand(~arr[0],~arr[0]*20)});
~synthArr.do{|item, index| item.set(\freq, ~arr[index].postln)};</pre>

<p><strong>Time-varying additive synthesis</strong>:<pre class='code prettyprint lang-sc'>(
SynthDef(\addSynth, {arg freq=40, amp=0.5, lag=0.01, atk=0.1,dec=0.2,sus=0.5,rel=1.0,gate=1, vol=0.05;
    var signal, env;
    signal = SinOsc.ar(freq.lag(lag),0,amp.lag(lag));
    env = EnvGen.kr(Env.adsr(atk,dec,sus,rel), gate);
    Out.ar([0,1],signal*env*vol);
}).send(s);
)

(
~size = 100; //100 change number of oscillators
~arr = Array.fill(~size, {rrand(200,10000)}).sort;
~envArr = [Array.rand(~size,0.1,2.6),Array.rand(~size,0.3,0.6), Array.rand(~size,0.8,1.0), Array.rand(~size,0.1,10.0)];
~synthArr = [];
~arr.do{|item, index|
    ~synthArr = ~synthArr.add(Synth(\addSynth, [\freq, item, \amp, (rrand(0.5,0.1)/(~arr.size)), \atk, ~envArr[0][index].postln, \dec, ~envArr[1][index], \sus, ~envArr[2][index], \rel, ~envArr[3][index], \lag, 0.1])); //change lag time
};

~rout = {inf.do{
    ~arr = [~arr[0]] ++ Array.fill(~arr.size-1,{rrand(~arr[0],~arr[0]*20)});
    ~synthArr.do{|item, index| item.set(\freq, ~arr[index], \amp, rrand(0.5,0.1))};
    0.01.yield; //change wait time
}}.fork;
)


~synthArr.do{|item, index| item.set(\gate, 0)};
~rout.stop;

~synthArr.do{|item, index| item.free};</pre>
<h2><a class='anchor' name='Subtractive%20Synthesis'>Subtractive Synthesis</a></h2>

<p><code class='code prettyprint lang-sc'>WhiteNoise</code> as source.<pre class='code prettyprint lang-sc'>{WhiteNoise.ar(1)!2}.scope;

FreqScope.new(400, 200, 0); //frequency scope</pre>
<h3><a class='anchor' name='Filters:'>Filters:</a></h3>

<p>Hi pass filter:<pre class='code prettyprint lang-sc'>{HPF.ar(WhiteNoise.ar(0.5)!2, MouseX.kr(20,22000))}.scope</pre>

<p>Low pass filter:<pre class='code prettyprint lang-sc'>{LPF.ar(WhiteNoise.ar(0.5)!2, MouseX.kr(20,22000))}.scope</pre>

<p>Resonant low pass filter:<pre class='code prettyprint lang-sc'>{RLPF.ar(WhiteNoise.ar(0.5)!2, MouseX.kr(20,22000), MouseY.kr(0.001,0.9))}.scope</pre>

<p>Resonant high pass filter:<pre class='code prettyprint lang-sc'>{RHPF.ar(WhiteNoise.ar(0.5)!2, MouseX.kr(20,22000), MouseY.kr(0.001,0.9))}.scope</pre>

<p>Band pass filter:<pre class='code prettyprint lang-sc'>{BPF.ar(WhiteNoise.ar(0.5)!2, MouseX.kr(440,17600), MouseY.kr(0.001,0.9))}.scope</pre>

<p>Band reject filter:<pre class='code prettyprint lang-sc'>{BRF.ar(WhiteNoise.ar(0.5)!2, 11000, MouseY.kr(0.01,1.9))}.scope</pre>

<p><strong>EQ Filters</strong>
<p>Highpass:<pre class='code prettyprint lang-sc'>{Pan2.ar(BHiPass.ar(WhiteNoise.ar(0.5), MouseX.kr(20,22000)),0)}.scope</pre>

<p>Lowshelf cut -20db:<pre class='code prettyprint lang-sc'>{Pan2.ar(BLowShelf.ar(WhiteNoise.ar(0.5)!2, MouseX.kr(20,22000), MouseY.kr(0.5,10.0), -20), 0)}.scope //reciprocal of s (slope) = 1/s</pre>

<p>Lowshelf boost 20db:<pre class='code prettyprint lang-sc'>{Pan2.ar(BLowShelf.ar(WhiteNoise.ar(0.1)!2, MouseX.kr(20,22000), MouseY.kr(0.5,10.0), 20), 0)}.scope //reciprocal of s (slope) = 1/s</pre>

<p>Parametric eq cut -10db:<pre class='code prettyprint lang-sc'>{Pan2.ar(BPeakEQ.ar(WhiteNoise.ar(0.5)!2, MouseX.kr(20,22000,1).poll, MouseY.kr(0.1,10.0), -10),0)}.scope //reciprocal of q (bandwidth / cutoffFreq)</pre>

<p>Parametric eq cut -20db:<pre class='code prettyprint lang-sc'>{Pan2.ar(BPeakEQ.ar(WhiteNoise.ar(0.5)!2, MouseX.kr(20,22000,1).poll, MouseY.kr(0.1,10.0), -20),0)}.scope //reciprocal of q (bandwidth / cutoffFreq)</pre>

<p>Parametric eq cut 10db:<pre class='code prettyprint lang-sc'>{Pan2.ar(BPeakEQ.ar(WhiteNoise.ar(0.25)!2, MouseX.kr(20,22000,1).poll, MouseY.kr(0.1,10.0), 10),0)}.scope //reciprocal of q (bandwidth / cutoffFreq)</pre>

<p>Parametric eq cut 20db:<pre class='code prettyprint lang-sc'>{Pan2.ar(BPeakEQ.ar(WhiteNoise.ar(0.1)!2, MouseX.kr(20,22000,1).poll, MouseY.kr(0.1,10.0), 20),0)}.scope //reciprocal of q (bandwidth / cutoffFreq)</pre>

<p>Hishelf cut -10db:<pre class='code prettyprint lang-sc'>{Pan2.ar(BHiShelf.ar(WhiteNoise.ar(0.5)!2, MouseX.kr(20,22000), MouseY.kr(0.5,10.0), -10), 0)}.scope //reciprocal of s (slope) = 1/s</pre>

<p>Hishelf cut -20db:<pre class='code prettyprint lang-sc'>{Pan2.ar(BHiShelf.ar(WhiteNoise.ar(0.5)!2, MouseX.kr(20,22000), MouseY.kr(0.5,10.0), -20),0)}.scope</pre>

<p>Hishelf boost 10db:<pre class='code prettyprint lang-sc'>{Pan2.ar(BHiShelf.ar(WhiteNoise.ar(0.25)!2, MouseX.kr(20,22000), MouseY.kr(0.5,10.0), 10),0)}.scope</pre>

<p>Hishelf boost 20db:<pre class='code prettyprint lang-sc'>{Pan2.ar(BHiShelf.ar(WhiteNoise.ar(0.1)!2, MouseX.kr(20,22000), MouseY.kr(0.5,10.0), 20), 0)}.scope</pre>

<p>Lowpass:<pre class='code prettyprint lang-sc'>{Pan2.ar(BLowPass.ar(WhiteNoise.ar(0.5)!2, MouseX.kr(20,22000)),0)}.scope</pre>

<p><strong>Multiband EQ</strong><pre class='code prettyprint lang-sc'>(
a = {arg lag=10, hpfreq=100,hpq=0.71,lsfreq=80,lsdb=10,lss=1.1,p1freq=43,p1db=0,
    p1q=6,p2freq=86,p2db=0,p2q=6,p3freq=129,p3db=0,p3q=6,p4freq=172,p4db=0,
    p4q=6,hsfreq=2000,hsdb=0,hss=0.1,lpfreq=20000,lpq=0.71,byp1=0,byp2=0,
    byp3=0,byp4=0,byp5=0,byp6=0,byp7=0,byp8=0,vol=0.5;
    var signal;
    signal = WhiteNoise.ar(0.25);
    signal = BHiPass.ar(signal,hpfreq.lag(lag),(1/hpq.lag(lag)))*byp1.linlin(0,1,1,0) + (signal*byp1);
    signal = BLowShelf.ar(signal,lsfreq.lag(lag),(1/lss.lag(lag)), lsdb.lag(lag))*byp2.linlin(0,1,1,0)
    + (signal*byp2);
    signal = BPeakEQ.ar(signal,p1freq.lag(lag),(1/p1q.lag(lag)), p1db.lag(lag))*byp3.linlin(0,1,1,0)
    + (signal*byp3);
    signal = BPeakEQ.ar(signal,p2freq.lag(lag),(1/p2q.lag(lag)),p2db.lag(lag))*byp4.linlin(0,1,1,0)
    + (signal*byp4);
    signal = BPeakEQ.ar(signal,p3freq.lag(lag),(1/p3q.lag(lag)),p3db.lag(lag))*byp5.linlin(0,1,1,0)
    + (signal*byp5);
    signal = BPeakEQ.ar(signal,p4freq.lag(lag),(1/p4q.lag(lag)),p4db.lag(lag))*byp6.linlin(0,1,1,0)
    + (signal*byp6);
    signal = BHiShelf.ar(signal,hsfreq.lag(lag),(1/hss.lag(lag)), hsdb.lag(lag))*byp7.linlin(0,1,1,0)
    + (signal*byp7);
    signal = BLowPass.ar(signal,lpfreq.lag(lag),(1/lpq.lag(lag)))*byp8.linlin(0,1,1,0)
    + (signal*byp8);
    Pan2.ar(signal,0)*vol;
}.scope;
)

a.set(\p1db,20, \p1q,10.0, \p1freq, 800);
a.set(\p2db,20, \p2q,10.0, \p2freq, 800*2);
a.set(\p3db,20, \p3q,10.0, \p3freq, 800*3);
a.set(\p4db,20, \p4q,10.0, \p4freq, 800*4);

a.set(\byp1, 1, \lsfreq, 82,\lsdb,10,\lss,0.2,\p1freq,220,\p1db, -10);

a.set(\hpfreq, 440, \hpq, 6)</pre>

<p>Resonance and ringing<pre class='code prettyprint lang-sc'>{Resonz.ar(WhiteNoise.ar(0.5)!2, MouseX.kr(20,22000), MouseY.kr(0.001,0.9))}.scope //resonant high

{Impulse.ar(MouseX.kr(0.5,10))!2*0.5}.scope

{Resonz.ar(Impulse.ar(1,0,20), MouseX.kr(440,10000), MouseY.kr(0.0001,0.01))!2*10}.scope //resonant filter ringing

{Ringz.ar(Impulse.ar(1,0,1), MouseX.kr(440,10000), 0.5)!2 }.scope //same but specify ring times</pre>

<p><code class='code prettyprint lang-sc'>Klank</code> and <code class='code prettyprint lang-sc'>DynKlank</code> are banks of resonators in SC.<pre class='code prettyprint lang-sc'>(
{
    Klank.ar([ // the multichannel-expansion
            `[[500, 1078, 1201.5, 1723], nil, [1, 1, 0.5, 0.3]], // left channel
            `[[700, 1071, 1053, 1723], nil, [1, 1, 1, 1]] // right channel
        ], Impulse.ar([1.5, 1.875], 0, 0.1))
}.play
)</pre>

<p>With <code class='code prettyprint lang-sc'>DynKlank</code> you can change parameters dynamically.<pre class='code prettyprint lang-sc'>(
// change freqs and ringtimes with mouse
{    var freqs, ringtimes;
        freqs = [800, 1071, 1153, 1723] * MouseX.kr(0.5, 2, 1);
        ringtimes = [1, 1, 1, 1] * MouseY.kr(0.1, 10, 1);
        DynKlank.ar(`[freqs, nil, ringtimes ], Impulse.ar(2, 0, 0.1))
}.play;
)

(
// set them from outside later:
SynthDef('help-dynKlank', {
        var freqs, ringtimes, signal;
        freqs = Control.names([\freqs]).kr([800, 1071, 1153, 1723]);
        ringtimes = Control.names([\ringtimes]).kr([1, 1, 1, 1]);
        signal = DynKlank.ar(`[freqs, nil, ringtimes ], Impulse.ar(2, 0, 0.1));
        Out.ar(0, signal);
}).add;
)

a = Synth('help-dynKlank');

a.setn(\freqs, Array.rand(4, 500, 2000));
a.setn(\ringtimes, Array.rand(4, 0.2, 4) );</pre>
<h3><a class='anchor' name='Noise'>Noise</a></h3>

<p>Periodicity vs noise:<pre class='code prettyprint lang-sc'>{SoundIn.ar(1)}.scope</pre>

<p>Different types of coloured noise<pre class='code prettyprint lang-sc'>{WhiteNoise.ar(0.5)!2}.scope

{WhiteNoise.ar(0.5)!2}.scope

{PinkNoise.ar(0.5)!2}.scope

{GrayNoise.ar(0.5)!2}.scope

{BrownNoise.ar(0.5)!2}.scope</pre>
<h2><a class='anchor' name='Multiple%20Wavetable%20Synthesis'>Multiple Wavetable Synthesis</a></h2>

<p><strong>Wavetable crossfading</strong><pre class='code prettyprint lang-sc'>(
SynthDef(\wavecross1, {arg freq=440, attack=0.01, dec=0.4, sus=0.3, sus2=0.4, rel=2, vol=0.5;
    var wave1, wave2, wave3, wave4;
    var env1,env2,env3,env4, globalEnv;
    env1 = EnvGen.kr(Env.new([0,1,0],[attack,dec]));
    env2 = EnvGen.kr(Env.new([0,0,1,0],[attack,dec,sus]));
    env3 = EnvGen.kr(Env.new([0,0,1,0],[attack+dec,sus,sus2]));
    env4 = EnvGen.kr(Env.new([0,0,1,0],[attack+dec+sus,sus2,rel]));
    globalEnv = EnvGen.kr(Env.new([0,1,0.5,0.5,0.4,0],[attack,dec,sus,sus2,rel]));

    wave1 = Pulse.ar(freq)*env1;
    wave2 = Formant.ar(freq)*env2;
    wave3 = Saw.ar(freq)*env3;
    wave4 = SinOsc.ar(freq)*env4;

    ////noise instead
    //wave1 = WhiteNoise.ar*env1;
    //wave2 = GrayNoise.ar*env2;
    //wave3 = PinkNoise.ar*env3;
    //wave4 = BrownNoise.ar*env4;

    Out.ar(0, Pan2.ar((wave1 + wave2 + wave3 + wave4))*globalEnv*vol);
}).send(s);
)

Synth(\wavecross1, [\freq, 440]);
Synth(\wavecross1, [\freq, 880]);


(
{12.do{
    Synth(\wavecross1, [\freq, (Scale.major.degrees+[60,72].choose).choose.midicps]);
    0.8.yield;
}}.fork;
)

(//detuned synths
{24.do{
    Synth(\wavecross1, [\freq, rrand(200,800), \attack, rrand(0.01,0.05), \dec, rrand(0.2,0.4), \sus,rrand(0.1,0.3), \sus2, rrand(0.1,0.3), \rel, rrand(0.4,2.1), \vol, rrand(0.1,0.5)]);
    0.2.yield;
}}.fork;
)</pre>

<p><strong>Wavestacking</strong><pre class='code prettyprint lang-sc'>(
SynthDef(\wavestack1, {arg freq=440, attack=0.1, sus=0.3, rel=2, vol=0.5;
    var globalEnv, harm, signal, envArr;
    var wave1, wave2, wave3, wave4;
    globalEnv = EnvGen.kr(Env.linen(attack,sus,rel), doneAction:2);
    [attack,sus,rel].sum*[[0.3,0.6,0.1],[0.1,0.5,0.4],[0.01,0.3,0.69],[0.8,0.1,0.1]].do{|item| envArr = envArr.add(Env.linen(item[0], item[1], item[2]))};
    harm = [1,2,3,4];
    wave1 = Pulse.ar(freq*harm[3])*EnvGen.kr(envArr[0]);
    wave2 = Formant.ar(freq*harm[1])*EnvGen.kr(envArr[1]);
    wave3 = Saw.ar(freq*harm[2])*EnvGen.kr(envArr[2]);
    wave4 = SinOsc.ar(freq*harm[0])*EnvGen.kr(envArr[3]);
    signal = ((wave1 + wave2 + wave3 + wave4)/4)*globalEnv;
    Out.ar(0, Pan2.ar(signal*vol*2));
}).send(s);
)

Synth(\wavestack1, [\freq, 440]);
Synth(\wavestack1, [\freq, 880]);

(
{12.do{
    Synth(\wavestack1, [\freq, (Scale.major.degrees+[60,72].choose).choose.midicps]);
    0.8.yield;
}}.fork;
)

(//detuned synths
{24.do{
    Synth(\wavestack1, [\freq, rrand(200,800), \attack, rrand(0.01,0.05), \dec, rrand(0.2,0.4), \sus,rrand(0.1,0.3), \sus2, rrand(0.1,0.3), \rel, rrand(0.4,2.1), \vol, rrand(0.1,0.5)]);
    0.2.yield;
}}.fork;
)</pre>
<h2><a class='anchor' name='Modulation%20Synthesis'>Modulation Synthesis</a></h2>
<h3><a class='anchor' name='Ring%20modulation'>Ring modulation</a></h3>

<p>Create a frequency analyzer:<pre class='code prettyprint lang-sc'>FreqScope.new(400, 200, 0);</pre>

<p>Simple ring modulation:<pre class='code prettyprint lang-sc'>{SinOsc.ar(440,0,SinOsc.ar(Line.kr(1,10,20)))*0.5}.scope; //low freq ring modulation causes tremolo</pre>

<p>This is the same (a multiplication of two bipolar signals):<pre class='code prettyprint lang-sc'>{SinOsc.ar(440,0,0.5)*SinOsc.ar(Line.kr(1,10,20))}.scope; //low freq ring modulation causes tremolo</pre>

<p>With a sound file:<pre class='code prettyprint lang-sc'>b = Buffer.loadDialog;

{PlayBuf.ar(1, b.bufnum)*SinOsc.ar(10)}.play //low freq causes tremolo

{PlayBuf.ar(1, b.bufnum)*SinOsc.ar(440)}.play //high freq causes sidebands

(
{var carrier, modulator;
    carrier = 1000;
    modulator = 400;
    SinOsc.ar(carrier,0,SinOsc.ar(modulator));
}.scope;
)</pre>

<p>Side bands result in: <em>sum of C and M (1000+400=1400) and difference of C and M (1000-400=600)</em><pre class='code prettyprint lang-sc'>(
{var carrier, modulator;
    carrier = 100;
    modulator = 400;
    SinOsc.ar(carrier,0,SinOsc.ar(modulator));
}.scope;
)</pre>

<p>Side bands result in: <em>sum of C and M (100+400=500) and difference of C and M (100-400=-300) - negative frequencies change the sign of the phase of signal</em><pre class='code prettyprint lang-sc'>(
{var carrier, modulator;
    carrier = 100;
    modulator = 400;
    SinOsc.ar(carrier,0,SinOsc.ar(modulator))+SinOsc.ar(300,MouseX.kr(0,-0.5pi));
}.scope;
)</pre>

<p>Harmonic sidebands:<pre class='code prettyprint lang-sc'>{(SinOsc.ar(220,0,0.25)+SinOsc.ar(440,0,0.25))*SinOsc.ar(880)}.scope; C and M are within harmonic ratios</pre>

<p>Inharmonic sidebands:<pre class='code prettyprint lang-sc'>{(SinOsc.ar(100,0,0.25)+SinOsc.ar(380,0,0.25))*SinOsc.ar(775)}.scope;</pre>

<p><h3><a class='anchor' name='Amplitude%20modulation:'>Amplitude modulation:</a></h3>
<pre class='code prettyprint lang-sc'>(
{var carrier, modulator;
    carrier = 500;
    modulator = 50;
    SinOsc.ar(carrier,0,SinOsc.ar(modulator, add:1, mul:0.5))*0.5;
}.scope;
)</pre>

<p>Side bands result in: <em>sum of C and M (1000+400=1400) and difference of C and M (1000-400=600) + C (1000)</em><pre class='code prettyprint lang-sc'>(
{var carrier, modulator;
    carrier = 1000;
    modulator = 400;
    SinOsc.ar(carrier,0,SinOsc.ar(modulator, add:1, mul:0.5))*0.5;
}.scope;
)</pre>

<p>In AM, high frequency causes sidebands but also mantains carrier frequency:<pre class='code prettyprint lang-sc'>b = Buffer.loadDialog;
{PlayBuf.ar(1, b.bufnum)*SinOsc.ar(440, add:1, mul:0.5)}.play</pre>

<p>In RM, high frequency causes sidebands:<pre class='code prettyprint lang-sc'>{PlayBuf.ar(1, b.bufnum)*SinOsc.ar(440)}.play</pre>
<h3><a class='anchor' name='Frequency%20modulation'>Frequency modulation</a></h3>

<p>Can be done (with out envelope) in the following way:<pre class='code prettyprint lang-sc'>(
var carrierFreq = 400, modFreq = 50, deviation = 100;
{    SinOsc.ar(carrierFreq + SinOsc.ar(modFreq, mul: deviation),
    mul: 0.3);
}.scope
)</pre>

<p>the same can be expressed:<pre class='code prettyprint lang-sc'>(
var carrierFreq = 400, modFreq = 50, deviation = 100;
{SinOsc.ar(SinOsc.ar(modFreq,mul: deviation, add: carrierFreq),
    mul: 0.3);
}.scope
)</pre>
<h3><a class='anchor' name='Phase%20modulation'>Phase modulation</a></h3>

<p>is similar, but we modulate the phase instead of the frequency:
<p><code class='code prettyprint lang-sc'>PMOsc.ar(carfreq, modfreq, index, modphase, mul, add)</code><ul>
<li><em>carfreq</em> - carrier frequency in cycles per second.<li><em>modfreq</em> - modulator frequency in cycles per second.<li><em>index</em> is modulation index in radians<li><em>modphase</em> a modulation input for the modulator phase in radians</ul>

<p><pre class='code prettyprint lang-sc'>{ PMOsc.ar(Line.kr(600, 900, 5), 600, 3, 0, 0.1) }.play; // modulates carfreq

{ PMOsc.ar(300, Line.kr(600, 900, 5), 3, 0, 0.1) }.play; // modulates modfreq

{ PMOsc.ar(300, 550, Line.ar(0,20,8), 0, 0.1) }.play; // modulates index

(
var carrierFreq = 400, modFreq = 50, index = 3, decayTime = 5;
{ EnvGen.kr(Env.perc(0.001,decayTime,0.2),1,doneAction: 2) *
    PMOsc.ar(carrierFreq, modFreq, index,0);
}.scope
)

(
var carrierFreq = 400, modFreq,cmRatio = 1.5, index = 3, decayTime = 5;
modFreq = (carrierFreq * cmRatio.reciprocal).postln;
{ EnvGen.kr(Env.perc(0.001,decayTime,0.2),1,doneAction: 2) *
    PMOsc.ar(carrierFreq, modFreq, index,0);
}.scope
)</pre>
<h2><a class='anchor' name='Dynamic%20waveshaping'>Dynamic waveshaping</a></h2>
<pre class='code prettyprint lang-sc'>(
a = Signal.sineFill(1000, [1, 0.2, 0.7]);
// or..
//a = Signal.sineFill(1000, [0, 0.2, 0.8, 0.1, 0.5]);
// the straight version…
// a = Array.interpolation(1000, 0.0, 1.0);
// check out the transfer function
a.plot;
)
// then…
(
b = Buffer.sendCollection(s, a, 1);
)

//wavetable modulation
(
{
    var sinFreq, soundIn, playHead, output;
    var thisIndex;
    sinFreq = MouseX.kr(20, 1000, \exponential).poll;
    soundIn = SinOsc.ar(sinFreq, 0, 0.8);
    //thisIndex = LinLin.ar(soundIn, -1.0, 1.0, 0.0, BufFrames.kr(b.bufnum));
    // some gentle dynamic waveshaping -
    // modulate the range of the indexing
    thisIndex = LinLin.ar(soundIn, -1.0, 1.0, 0.0, BufFrames.kr(b.bufnum) * MouseY.kr(0.1,1));
    // thisIndex = LinLin.ar(soundIn, -1.0, 1.0, 0.0, BufFrames.kr(b.bufnum) * SinOsc.kr(0.6).range(0.15, 1.0));
    // some over-aggressive modulation!
    // thisIndex = LinLin.ar(soundIn, -1.0, 1.0, 0.0, BufFrames.kr(b.bufnum) * SinOsc.ar(sinFreq * 0.25).range(0.15, 1.0));
    playHead = BufRd.ar(1, b.bufnum, thisIndex, 0, 4);
    // remove any DC weirdness
    output = LeakDC.ar(playHead);
    output ! 2;
}.scope;
)

(
{
    var sinFreq, soundIn, playHead, output;
    var thisIndex;
    var sinMult, sinOffsetRange;
    sinFreq = MouseX.kr(20, 2000, \exponential);
    // now move the base position of the sine around, so that different areas of the transfer function get used
    // resulting in a nice shifting around of the phase
    sinMult = 0.32; sinOffsetRange = 0.64;
    soundIn = SinOsc.ar(sinFreq, 0, sinMult, SinOsc.kr(0.17).range(0.0 - sinOffsetRange, sinOffsetRange));
    thisIndex = LinLin.ar(soundIn, -1.0, 1.0, 0.0, BufFrames.kr(b.bufnum));
    playHead = BufRd.ar(1, b.bufnum, thisIndex, 0, 4);
    // remove any DC weirdness
    output = LeakDC.ar(playHead);
    output ! 2;
}.scope;
)


(
{
    var sinFreq, soundIn, playHead, output;
    var thisIndex;
    var bufInput, bufInputFreq;
    sinFreq = MouseX.kr(20, 1000, \exponential).poll;
    soundIn = SinOsc.ar(sinFreq, 0, 0.8);
    thisIndex = LinLin.ar(soundIn, -1.0, 1.0, 0.0, BufFrames.kr(b.bufnum));
    // fill one cycle of the buffer without glitches -
    // the frequency should be a period that fits into the size of the buffer,
    // according to the sample rate
    bufInputFreq = SampleRate.ir / BufFrames.kr(b.bufnum);
    // mix the first 6 tones of the harmonic series – let them fade in and out randomly
    bufInput = Mix(
        SinOsc.ar(bufInputFreq * (1 .. 6), Array.linrand(6, 0.0, 6.28), SinOsc.kr(Array.exprand(6, 0.1, 0.3), 0, 0.2))
    );
    // write this into the buffer that is being used as the transfer function
    BufWr.ar(bufInput, b.bufnum, Phasor.ar(0, BufRateScale.kr(b.bufnum), 0, BufFrames.kr(b.bufnum)));
    playHead = BufRd.ar(1, b.bufnum, thisIndex, 0, 4);
    // remove any DC weirdness
    output = LeakDC.ar(playHead);
    // uncomment to check out the transfer function
    // output = BufRd.ar(1, b.bufnum, Phasor.ar(0, BufRateScale.kr(b.bufnum), 0, BufFrames.kr(b.bufnum)), 0, 4);
    output ! 2;
}.scope;
)

(
{
    var sinFreq, soundIn, playHead, output;
    var thisIndex;
    var bufInput, bufInputFreq;
    var modPoints, env, envgen;
    sinFreq = MouseX.kr(20, 1000, \exponential).poll;
    soundIn = SinOsc.ar(sinFreq, 0, 0.8);
    thisIndex = LinLin.ar(soundIn, -1.0, 1.0, 0.0, BufFrames.kr(b.bufnum));
    bufInputFreq = SampleRate.ir / BufFrames.kr(b.bufnum);
    // use these points for the env levels – one extra last one (release node) so that the env loops properly
    modPoints = [-1.0, -0.2, -0.5, 0.9, 1.0, 1.0];
    // uncomment to see the points
    // modPoints.plot;
    // uncomment to replace the points with modulating values
    5.do { |i|
        modPoints[i] = SinOsc.kr(ExpRand(0.1, 0.8), Rand(0.0, 6.28)).range(-0.9, 0.9);
    };
    // make an envelope which will result in a warped transfer function
    env = Env(modPoints, [0.25, 0.25, 0.25, 0.25, 0.0], [2, -4, 7, -5, 0], 4, 0);
    envgen = EnvGen.ar(env, timeScale: bufInputFreq.reciprocal);
    bufInput = envgen;
    // write this into the buffer that is being used as the transfer function
    BufWr.ar(bufInput, b.bufnum, Phasor.ar(0, BufRateScale.kr(b.bufnum), 0, BufFrames.kr(b.bufnum)));
    playHead = BufRd.ar(1, b.bufnum, thisIndex, 0, 4);
    // remove any DC weirdness
    output = LeakDC.ar(playHead);
    // uncomment to check out the transfer function
    // output = BufRd.ar(1, b.bufnum, Phasor.ar(0, BufRateScale.kr(b.bufnum), 0, BufFrames.kr(b.bufnum)), 0, 4);
    output ! 2;
}.scope;
)</pre>
<h2><a class='anchor' name='WaveTerrain%20Synthesis.'>WaveTerrain Synthesis.</a></h2>

<p>WaveTerrain UGen by Nick Collins - part of SLUGens plugins (sc3-plugins)<pre class='code prettyprint lang-sc'>//create buffer. I want the equation z = 2*(((x/100)**2) + ((abs(sin(10*y))/50)**(1/3)))-1
//over a 100 by 50 area

//2d to 1d conversion follows index= y*rowlength+ x
(
var width= 100; //= num cols
var height=50; //=num rows, though indexing bottom to top; i.e., standard Cartesian co-ordinates

a=Array.fill(width*height,{arg i;
    var xnow, ynow, x, y;

    xnow= i%width;
    ynow= (i-xnow).div(width);

    x=xnow/width;
    y=ynow/height;

    2*(((x)**2) + ((abs(sin(10*y)))**(1/3)))-1

});


b=Buffer.sendCollection(s, a, 1);
)
//plot buffer
b.plot;


//LFNoise adds some drift to explore the landscape more
{WaveTerrain.ar(b.bufnum,SinOsc.ar(MouseX.kr(1,200,'exponential')).abs + LFNoise2.ar(2),SinOsc.ar(MouseY.kr(1,300,'exponential'),pi*0.5).abs,100,50)*0.2}.scope


//change surface equation
(
var width= 100; //= num cols
var height=50; //=num rows, though indexing bottom to top; i.e., standard Cartesian co-ordinates

a=Array.fill(width*height,{arg i;
    var xnow, ynow, x, y;

    xnow= i%width;
    ynow= (i-xnow).div(width);

    x=xnow/width;
    y=ynow/height;

    (((cos(5*x+1.7))**3) - ((abs(sin(23*y)))**(1/3)))

});

b.sendCollection(a);
)
b.plot;


//change surface equation
(
var width= 100; //= num cols
var height=50; //=num rows, though indexing bottom to top; i.e., standard Cartesian co-ordinates

a=Array.fill(width*height,{arg i;
    var xnow, ynow, x, y;

    xnow= i%width;
    ynow= (i-xnow).div(width);

    x=xnow/width;
    y=ynow/height;

    (((1.3*(cos(rrand(1,2)*x+1.7))**2) - ((abs(sin(rrand(1.2,4.9)*y)))**(1/2)))).max(-1.0).min(1.0)

});

b.sendCollection(a);
)
b.plot;</pre>
<div class='doclink'>helpfile source: <a href='file:///Users/federicoreuben/Library/Application Support/SuperCollider/Extensions/ComputerPlay/HelpSource/Workshops/Playing with UGens 2.schelp'>/Users/federicoreuben/Library/Application Support/SuperCollider/Extensions/ComputerPlay/HelpSource/Workshops/Playing with UGens 2.schelp</a><br>link::Workshops/Playing with UGens 2::<br>sc version: 3.8.0</div></div></body></html>