<html><head><title>Buffer Tricks</title>
<link rel='stylesheet' href='./../../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../../scdoc.js' type='text/javascript'></script>
<script src='./../../docmap.js' type='text/javascript'></script>
<script src='./../../prettify.js' type='text/javascript'></script>
<script src='./../../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./../..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider SSFMT/CODE EXAMPLES (extension)</div>
<div id='categories'><a href='./../../Browse.html#Tutorials/SSfMT'>Tutorials/SSfMT</a></div>
<h1>Buffer Tricks</h1>
<div id='summary'>Software Systems for Music Technology</div>
</div>
<div class='subheader'>
<div id='related'>See also: <a href="./../../SSfMT.html">Software Systems for Music Technology</a>, <a href="./../../SSfMT/Lectures/Digital Synthesis.html">Digital Synthesis</a>, <a href="./../../SSfMT/Study Guides/Synthesis in SuperCollider.html">Synthesis in SuperCollider</a></div>
</div>
<div id='toc'>
<ul class='toc'><li class='toc1'><a href='#Recording'>Recording</a></li>
<ul class='toc'><li class='toc2'><a href='#Buffers'>Buffers</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#SoundIn%20(Read%20from%20audio%20input)'>SoundIn (Read from audio input)</a></li>
<ul class='toc'></ul><li class='toc2'><a href='#RecordBuf%20(Play%20from%20Buffer)'>RecordBuf (Play from Buffer)</a></li>
<ul class='toc'></ul></ul><li class='toc1'><a href='#Granular%20Synthesis'>Granular Synthesis</a></li>
<ul class='toc'></ul><li class='toc1'><a href='#Many%20SoundFiles%20into%20many%20Buffers'>Many SoundFiles into many Buffers</a></li>
<ul class='toc'></ul></ul></div><h2><a class='anchor' name='Recording'>Recording</a></h2>

<p>Play something...<pre class='code prettyprint lang-sc'>s.boot;

a = {Pan2.ar(SinOsc.ar(SinOsc.kr(MouseX.kr(1,2000), mul: MouseY.kr(200,4000), add: MouseY.kr(200,4000)), 0, 0.2), SinOsc.kr(MouseX.kr(1,10))) }.play;</pre>

<p>The easiest way to record is with the record button in the server window<ul>
<li>start recording (press record button)<li>wait a bit<li>stop recording (press button again)<li>Now, go to the folder Muisc/SuperCollider Recordings<li>The recording is there!</ul>

<p>Sometimes, when recording (but not only!), it is useful to see your input and output levels. You can do that if click on the server window and then press “l”. A window with the inputs and outputs will show up<h3><a class='anchor' name='Buffers'>Buffers</a></h3>

<p>Buffers are bits of data allocated in your computer memory. In order to manipulate sound files, we need to allocate the data from the sound file in the computer memory
<p>Buffers can contain all sort of data (not only soundfiles) and it is stored as an Array (collection) of foating-point numbers
<p>check the Buffer help file for more information<pre class='code prettyprint lang-sc'>s.boot; //first boot server

a = Buffer.loadDialog; //load a soundfile into a buffer;
a.plot;

a.getn(10*44100, 512, {|msg| ~array = msg}); //get 512 float numbers and store them in a global variable as an array</pre>

<p>Post &lt;&lt; ~array; // post the whole array!
<p><strong>PlayBuf  (Play from Buffer)</strong>
<p>PlayBuf is a UGen we use to play buffers<pre class='code prettyprint lang-sc'>a = Buffer.loadDialog; //load a soundfile into a buffer;

{PlayBuf.ar(a.numChannels, a.bufnum)}.play;

a.free; //after finishing, you have to release the buffer to release the memory</pre>

<p>if you want to specify the path from the soundfile, then you can use the message “read”
<p>if you want to get the specific path for a soundfile, simply drag the file into this document<pre class='code prettyprint lang-sc'>a = Buffer.read(s, "/Users/freuben/Music/MastersExamples/01 15 Steps.wav"); //fixed path (your own)

{PlayBuf.ar(a.numChannels, a.bufnum, BufRateScale.kr(a.bufnum))}.play;

{PlayBuf.ar(a.numChannels, a.bufnum, MouseX.kr(0.2,4.0))}.play; //change play rate

{PlayBuf.ar(a.numChannels, a.bufnum, MouseX.kr(0.2,4.0), Impulse.kr(MouseY.kr(0.1,10)), MouseY.kr(0,a.numFrames), loop:1)}.play; //loop, trigger and startPos

b = {{Pan2.ar(Mix.ar(PlayBuf.ar(a.numChannels, a.bufnum, Rand(0.1,4.0), Impulse.kr(Rand(0.1,10)), Rand(0,a.numFrames), loop:1))*EnvGen.kr(Env.perc(0.01*Rand(0.5,2.0), 0.5*Rand(0.5,2.0)), doneAction:2), Rand(-1,1.0))}.play;} //loop, trigger and startPos

~tempoMul = 1;
{inf.do{b.value; (~tempoMul*rrand(0.01,0.4)).yield}}.fork;
~tempoMul = 0.1;
~tempoMul = 1.2;</pre>
<h3><a class='anchor' name='SoundIn%20(Read%20from%20audio%20input)'>SoundIn (Read from audio input)</a></h3>

<p>SoundIn reads an input from your computer<pre class='code prettyprint lang-sc'>{SoundIn.ar(0)}.play //be careful with feedback (wear headphones)

{SoundIn.ar(0)*SinOsc.ar(440)}.play //simple ring modulation

//bathroom reverb
a = {arg roomsize=5, revtime=0.6, damping=0.62, inputbw=0.48, spread = 15, drylevel= -6, earlylevel= -11, taillevel= -13, wet=0.3, dry=1.0;
    var input, wetSignal, drySignal, output;
    input = Pan2.ar(SoundIn.ar(0), 0);
    wetSignal = GVerb.ar(input, roomsize, revtime, damping, inputbw, spread, drylevel.dbamp, earlylevel.dbamp, taillevel.dbamp, roomsize, wet);
    drySignal = (input*dry);
    output = wetSignal + drySignal;
}.play

//change it to a living room
a.set(\roomsize, 16, \revtime, 1.24, \damping, 0.10, \inputbw, 0.95, \drylevel -3, \earlylevel, -15, \taillevel, -17);

a.set(\wet, 0.8);

a.set(\wet, 0.3);

//church
a.set(\roomsize, 80, \revtime, 4.85, \damping, 0.41, \inputbw, 0.19, \drylevel -3, \earlylevel, -9, \taillevel, -11);

//cathedral
a.set(\roomsize, 243, \revtime, 5, \damping, 0.1, \inputbw, 0.34, \drylevel -3, \earlylevel, -11, \taillevel, -9);

a.set(\dry, 0.3);
a.set(\wet, 1.0);</pre>
<h3><a class='anchor' name='RecordBuf%20(Play%20from%20Buffer)'>RecordBuf (Play from Buffer)</a></h3>

<p>Allocate an empty buffer to record into<pre class='code prettyprint lang-sc'>b = Buffer.alloc(s, 44100 * 4.0, 1); // a four second 1 channel Buffer</pre>

<p>Record for four seconds<pre class='code prettyprint lang-sc'>(
{var input, nooutput=0;
    input = SoundIn.ar(0); //microphone at first input
    RecordBuf.ar(input, b, doneAction: 2, loop: 0);
    nooutput;
}.play
)</pre>

<p>Play it back<pre class='code prettyprint lang-sc'>{PlayBuf.ar(1,b, doneAction: 2);}.play;</pre>

<p>Overdub<pre class='code prettyprint lang-sc'>(
{var input, nooutput=0;
    input = SoundIn.ar(0); //microphone at first input
      RecordBuf.ar(input, b, 0, 0.5, 0.5, doneAction: 2, loop: 0);
    nooutput;
}.play
)</pre>

<p>Play it back<pre class='code prettyprint lang-sc'>{Pan2.ar(PlayBuf.ar(1,b, loop: 1),0);}.play;

b.plot;</pre>

<p>Write the contents of the buffer to a file (see Buffer for more options)<pre class='code prettyprint lang-sc'>b.write;</pre>

<p>Now lets try to use loop in RecordBuf<pre class='code prettyprint lang-sc'>// play it back
{Pan2.ar(PlayBuf.ar(1,b, loop: 1),0);}.play;</pre>

<p>Overdub with loop<pre class='code prettyprint lang-sc'>(
a = {arg loop=1;
    var input, nooutput=0;
    input = SoundIn.ar(0); //microphone at first input
      RecordBuf.ar(input, b, 0, 0.5, 0.5, doneAction: 2, loop: loop);
    nooutput;
}.play
)

a.set(\loop, 0); //stop overdubing

a.set(\loop, 1); //start overdubing again


b.free; //free buffer</pre>
<h2><a class='anchor' name='Granular%20Synthesis'>Granular Synthesis</a></h2>
<pre class='code prettyprint lang-sc'>~path = "/Users/federicoreuben/Music/MastersExamples/01 15 Steps.wav";
b = Buffer.readChannel(s, ~path, channels: [0]); //read just the first channel of soundfile

Ndef(\grains).play;
Ndef(\grains).fadeTime = 1;

(
//simple example
Ndef(\grains, {var rate = 50, amp = 0.2, grainDur=0.005;
    TGrains.ar(2,Impulse.kr(rate), b.bufnum, 1,
        Line.kr(0, b.duration, b.duration, doneAction: 2),grainDur,0,amp,1);
})
)</pre>

<p>With Dseq:<pre class='code prettyprint lang-sc'>(
Ndef(\grains, {
    var trate, dur, rate;
    trate = MouseY.kr(2,200,1);
    dur = 4 / trate;
    rate = Dseq([10, 1, 1, 0.5, 0.5, 0.2, 0.1], inf);
    TGrains.ar(2, Impulse.ar(trate), b, rate, MouseX.kr(0,BufDur.kr(b)),
        dur, Dseq([-1, 1], inf), 0.1, 2);
});
)</pre>

<p>Mouse determines trate and position<pre class='code prettyprint lang-sc'>(
Ndef(\grains, {
    var trate, dur, clk, pos, pan;
    trate = MouseY.kr(8,120,1);
    dur = 12 / trate;
    clk = Impulse.kr(trate);
    pos = MouseX.kr(0,BufDur.kr(b)) + TRand.kr(0, 0.01, clk);
    pan = WhiteNoise.kr(0.6);
    TGrains.ar(2, clk, b, 1, pos, dur, pan, 0.1);
});
)</pre>

<p>Experimenting with trate<pre class='code prettyprint lang-sc'>(
Ndef(\grains, {
    var trate, dur, clk, pos, pan;
    trate = LinExp.kr(LFTri.kr(MouseY.kr(0.1,2,1)),-1,1,8,120);
    dur = 12 / trate;
    clk = Impulse.ar(trate);
    pos = MouseX.kr(0,BufDur.kr(b));
    pan = WhiteNoise.kr(0.6);
    TGrains.ar(2, clk, b, 1, pos, dur, pan, 0.1);
});
)</pre>

<p>Noisy rate<pre class='code prettyprint lang-sc'>(
Ndef(\grains, {
    var trate, dur, clk, pos, pan;
    trate = MouseY.kr(1,400,1);
    dur = 8 / trate;
    clk = Impulse.kr(trate);
    pos = MouseX.kr(0,BufDur.kr(b));
    pan = WhiteNoise.kr(0.8);
    TGrains.ar(2, clk, b, 2 ** WhiteNoise.kr(2), pos, dur, pan, 0.1);
});
)</pre>

<p>Play with duration<pre class='code prettyprint lang-sc'>(
Ndef(\grains, {
    var trate, dur;
    trate = MouseY.kr(2,120,1);
    dur = 1.2 / trate;
    TGrains.ar(2, Impulse.ar(trate), b, (1.2 ** WhiteNoise.kr(3).round(1)),
        MouseX.kr(0,BufDur.kr(b)), dur, WhiteNoise.kr(0.6), 0.1);
});
)</pre>

<p>Demand ugens as inputs<pre class='code prettyprint lang-sc'>(
Ndef(\grains, {
    var trate, dur, z, d;
    trate = MouseX.kr(1, 100, 1);
    d = { Dwhite(0.1, 0.2, 1) };
    z = {
        Drand([Dgeom(0.1, 1 + d.value, Diwhite(20, 40)),
            Dgeom(1, 1 - d.value, Diwhite(20, 40))])
    };
    TGrains.ar(2,
        Impulse.ar(trate),
        bufnum: b,
        rate: Dseq([1, 1, z.value, 0.5, 0.5, 0.2, 0.1, 0.1, 0.1, 0.1], inf) * 2 + 1,
        centerPos: Dseq(z.dup(8), inf),
        dur: Dseq([1, d.value, 1, z.value, 0.5, 0.5, 0.1, z.value] * 2, inf) / trate,
        pan: Dseq([1, 1, 1, 0.5, 0.2, 0.1, 0, 0, 0], inf) * 2 - 1,
        amp: Dseq([1, 0, z.value, 0, 2, 1.0, 1, 0.1, 0.1], inf)
    )*0.6;
});
)</pre>

<p>Change rate and get minimalism<pre class='code prettyprint lang-sc'>(
Ndef(\grains, {var bdur = 10, rate = 1, amp = 0.2, stretch = 20, grainDur, trigger;
grainDur = 1/rate * stretch * 2.0;
    trigger = Impulse.kr(rate);
     Mix.fill(5,{TGrains.ar(2,trigger, b.bufnum, 1,Line.kr(0.3.rand,bdur + 0.3.rand, bdur * stretch, doneAction: 2) + TRand.kr(0,1/(bdur*stretch),trigger),grainDur,0,amp,1);})
});
)</pre>

<p>Trigger with dust<pre class='code prettyprint lang-sc'>(
Ndef(\grains, {var bdur = 10, rate = 1, amp = 0.2, stretch, grainDur, trigger;
    trigger = Dust.kr(rate);
    stretch = MouseX.kr(1, 20);
    grainDur = 1/rate * stretch * 2.0;
     Mix.fill(5,{TGrains.ar(2,trigger, b.bufnum, TRand.kr(2.0,0.5, trigger), Line.kr(0.3.rand,bdur + 0.3.rand, bdur * stretch, doneAction: 2) + TRand.kr(0,1/(bdur*stretch),trigger),grainDur,0,amp,1);})
})
)</pre>

<p>Multiple layers of triggering with Dust<pre class='code prettyprint lang-sc'>// make Synthdef
(
SynthDef(\dustGrains, { |bufnum = 0, out = 0, trate = 50, amp = 0.3, grainDur=0.05|
    var trigger, bdur;
    trigger = Dust.kr(trate);
    bdur = b.duration;
    Out.ar(0, TGrains.ar(2, trigger, b.bufnum, 1, TRand.kr(0,1,trigger) * bdur, TRand.kr(0.00001,MouseX.kr(0.0001,0.05),trigger),TRand.kr(-1,1,trigger))*amp)
}).load(s)
)</pre>

<p>Start multiple instances:<pre class='code prettyprint lang-sc'>Ndef(\grains).put(0, \dustGrains, extraArgs: [\trate,5]);
Ndef(\grains).put(1, \dustGrains, extraArgs: [\trate,6]);
Ndef(\grains).put(2, \dustGrains, extraArgs: [\trate,4]);
Ndef(\grains).put(3, \dustGrains, extraArgs: [\trate,9]);
Ndef(\grains).put(4, \dustGrains, extraArgs: [\trate,2]);


Ndef(\grains).clear;

~grains = 100;
~grains.do{ Synth(\dustGrains,[\trate,rrand(1,10), \amp, 15/~grains]);}</pre>
<h2><a class='anchor' name='Many%20SoundFiles%20into%20many%20Buffers'>Many SoundFiles into many Buffers</a></h2>

<p>Load lots of SoundFiles into Buffers in one go through the power of Collections<pre class='code prettyprint lang-sc'>(
~paths = ["/Users/fr155035/Music/MastersExamples/01 15 Steps.wav", "/Users/fr155035/Music/MastersExamples/1 Earth Intruders.aiff", "/Users/fr155035/Music/MastersExamples/02 Bodysnatchers.wav", "/Users/fr155035/Music/MastersExamples/2 Nicotine &amp; Gravy.aiff", "/Users/fr155035/Music/MastersExamples/3 Sublation.aiff", "/Users/fr155035/Music/MastersExamples/3 The Dull Flame Of Desire.aiff", "/Users/fr155035/Music/MastersExamples/04 Weird Fishes_Arpeggi.wav", "/Users/fr155035/Music/MastersExamples/7 Broken Train.aiff", "/Users/fr155035/Music/MastersExamples/07 Reckoner.wav", "/Users/fr155035/Music/MastersExamples/8 Milk &amp; Honey.aiff", "/Users/fr155035/Music/MastersExamples/9 Beautiful Way.aiff", "/Users/fr155035/Music/MastersExamples/10 My Juvenile.aiff", "/Users/fr155035/Music/MastersExamples/11 Debra.aiff"];
)

s.boot;

(
~buffers = [];
c = Condition.new;
{~paths.do{|item|
    c.test=false;
    ~buffers = ~buffers.add(Buffer.read(s, item, action: {c.test = true; c.signal}).postln);
    c.wait;}}.fork;
)

~buffers[5]
//play all of them, one after the other:
(
{~buffers.do{|item|
{PlayBuf.ar(item.numChannels, item.bufnum)}.play;
    item.duration.yield;}}.fork;
)

//play all of them at the same time:
(
{~buffers.do{|item|
{PlayBuf.ar(item.numChannels, item.bufnum)/~buffers.size}.play;}}.fork
)</pre>
<div class='doclink'>helpfile source: <a href='file:///Users/federicoreuben/Library/Application Support/SuperCollider/Extensions/SSfMT/HelpSource/SSfMT/Code Examples/Buffer Tricks.schelp'>/Users/federicoreuben/Library/Application Support/SuperCollider/Extensions/SSfMT/HelpSource/SSfMT/Code Examples/Buffer Tricks.schelp</a><br>link::SSfMT/Code Examples/Buffer Tricks::<br>sc version: 3.8.0</div></div></body></html>