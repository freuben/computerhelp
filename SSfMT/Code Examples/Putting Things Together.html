<html><head><title>Putting Things Together</title>
<link rel='stylesheet' href='./../../scdoc.css' type='text/css' />
<link rel='stylesheet' href='./../../frontend.css' type='text/css' />
<link rel='stylesheet' href='./../../custom.css' type='text/css' />
<meta http-equiv='Content-Type' content='text/html; charset=UTF-8' />
<script src='./../../scdoc.js' type='text/javascript'></script>
<script src='./../../docmap.js' type='text/javascript'></script>
<script src='./../../prettify.js' type='text/javascript'></script>
<script src='./../../lang-sc.js' type='text/javascript'></script>
<script type='text/javascript'>var helpRoot='./../..';</script>
</head>
<ul id='menubar'></ul>
<body onload='fixTOC();prettyPrint()'>
<div class='contents'>
<div class='header'>
<div id='label'>SuperCollider SSFMT/CODE EXAMPLES (extension)</div>
<div id='categories'><a href='./../../Browse.html#Tutorials/SSfMT'>Tutorials/SSfMT</a></div>
<h1>Putting Things Together</h1>
<div id='summary'>Software Systems for Music Technology</div>
</div>
<div class='subheader'>
<div id='related'>See also: <a href="./../../SSfMT.html">Software Systems for Music Technology</a>, <a href="./../../SSfMT/Lectures/Digital Effects.html">Digital Effects</a>, <a href="./../../SSfMT/Practicals/Buses and Ndefs.html">Buses and Ndefs</a></div>
</div>
<div id='toc'>
<ul class='toc'><li class='toc1'><a href='#Organising%20your%20code.'>Organising your code.</a></li>
<ul class='toc'></ul></ul></div><h2><a class='anchor' name='Organising%20your%20code.'>Organising your code.</a></h2>
<pre class='code prettyprint lang-sc'>(
s.waitForBoot({ //boots server
//routine function (.fork) to evaluate your code:
{
//replace this with the paths for the buffers you want to use:
~paths = [
"/Users/federicoreuben/Music/MastersExamples/01 15 Steps.wav",
"/Users/federicoreuben/Music/MastersExamples/1 Earth Intruders.aiff",
"/Users/federicoreuben/Music/MastersExamples/02 Bodysnatchers.wav",
"/Users/federicoreuben/Music/MastersExamples/2 Nicotine &amp; Gravy.aiff",
"/Users/federicoreuben/Music/MastersExamples/3 Sublation.aiff",
"/Users/federicoreuben/Music/MastersExamples/3 The Dull Flame Of Desire.aiff",
"/Users/federicoreuben/Music/MastersExamples/04 4 bit 9d api+e+6 [126.26].wav",
"/Users/federicoreuben/Music/MastersExamples/05 180db_ [130].wav"
];
//load your buffers for playing sound files
~buffers = [];
~paths.do{|item|
        ~buffers = ~buffers.add(Buffer.read(s, item) );
    s.sync; //wait for buffers to load
};
//your other buffers (for example for wavetables)
~waveBuffer = Buffer.alloc(s, 4096, 1);
~waveBuffer.sine1(1.0/(1,3..1000), true, true, true); //fill it up


s.sync; //wait for buffers to load

//your synthdefs for patterns:
SynthDef(\eightoeightkick,{|out= 0 freq = 440 bend = 0 amp = 0.1 ringtime=10.0
        reltime=1.0 dist = 0.1 pan=0.0|
        var impulse, filter, env;
        impulse = Impulse.ar(0);
        filter = Ringz.ar(impulse,XLine.ar((freq*bend.lag(0.1).midiratio),60,0.1),ringtime);
        env = Env.perc(0.001,reltime).kr(2);
        filter = (1.0-dist)*filter + (dist*(filter.distort));
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
    }).add;

SynthDef(\handclapsmash,{|out= 0 amp = 0.1 gate=1 cutoff= 12000 rq=0.5
        reltime=0.5 pan=0.0|
        var noise, filter, filterenv, env;
        noise = WhiteNoise.ar;
        filterenv = Env([0.0,1.0,0.3,0.0],[0.0,0.3,0.3]).kr;
        filter = BHiPass.ar(RLPF.ar(noise,cutoff*filterenv,rq),150);
        filter = BPeakEQ.ar(filter,440,1.0,8);
        filter = BPeakEQ.ar(filter,1200,1.0,14);
        env = Env.perc(0.005,reltime).kr(2);
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
 }).add;

SynthDef(\bassfoundation,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        cutoff= 1000 rq=0.5 pan=0.0|
        var osc, filter, env, filterenv;
        osc = Saw.ar((freq*bend.lag(0.1).midiratio));
        filterenv = Env.adsr(0.0,0.5,0.2,0.2).kr(2,gate);
        filter =  RLPF.ar(osc,cutoff*filterenv+100,rq);
        env = Env.adsr(0.01,0.0,0.9,0.05).kr(2,gate);
        Out.ar(out,Pan2.ar(filter*env*amp*2,pan));
}).add;

SynthDef(\prophet5pwmstrings,{|out= 0 freq = 440 bend = 0 amp = 1.0 gate=1
        lforate = 10 lfowidth= 0.5 cutoff= 12000 rq=0.5 pan = 0.0|
        var lfo, pulse, filter, env;
        lfo = LFTri.kr(lforate*[1,1.01],Rand(0,2.0)!2);
        pulse = Pulse.ar((freq*bend.lag(0.1).midiratio)*[1,1.01],lfo*lfowidth+0.5);
        filter = RLPF.ar(pulse,cutoff,rq);
        env = Env.adsr(0.01,0.0,1.0,0.5).kr(2,gate);
        Out.ar(out,Pan2.ar(Mix(filter)*env*amp*0.5,pan));
}).add;

SynthDef(\bassfoundation,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        cutoff= 1000 rq=0.5 pan=0.0|
        var osc, filter, env, filterenv;
        osc = Saw.ar((freq*bend.lag(0.1).midiratio));
        filterenv = Env.adsr(0.0,0.5,0.2,0.2).kr(2,gate);
        filter =  RLPF.ar(osc,cutoff*filterenv+100,rq);
        env = Env.adsr(0.01,0.0,0.9,0.05).kr(2,gate);
        Out.ar(out,Pan2.ar(filter*env*amp*2,pan));
}).add;

SynthDef(\plastickystrings,{|out= 0 freq = 440 bend = 0 amp = 0.1 gate=1
        lforate = 5900 lfowidth= 0.01 cutoff= 12000 rq=0.5 pan=0.0|
        var lfo, saw, filter, env;
        lfo = LFTri.ar(lforate,Rand(0,2.0));
        saw = Saw.ar((freq*bend.lag(0.1).midiratio)*(1.0+(lfowidth*lfo)),0.5);
        filter = BHiPass.ar(saw,(freq*bend.lag(0.1).midiratio),rq);     //or using cutoff
        env = Env.adsr(0.4,0.0,1.0,1.0).kr(2,gate);
        Out.ar(out,Pan2.ar(filter*env*amp,pan));
 }).add;

//your synth functions for Ndefs

~loopBuffer = {arg bufnum, rate=1, amp=0.5;
            PlayBuf.ar(2, bufnum, rate, loop: 1)*amp;
}; //loop stereo file and mix it

~abstractBuffer = {arg bufnum, rate=1, amp=0.5, duration=10, lo=0.4, hi=2;
            var sig;
            sig = PlayBuf.ar(2, bufnum, LFNoise1.kr(rate).range(lo,hi), Dust.kr(SinOsc.kr(rate).range(0.4,2)), TRand.kr(0, 44100*duration, Impulse.kr(rate)), loop: 1);
            sig*amp;
}; //playing with playbuf

~oscSynth = {arg bufnum=0, freq=200, amp=0.5, lfofreq=0.5, cutoffset=4, pan=0;
            var sig;
            sig = Osc.ar(bufnum, SinOsc.kr(freq, 0, SinOsc.ar(lfofreq).range(0,1)));
            sig = HPF.ar(sig, freq.lag(5)*cutoffset.lag(5));
            sig = Pan2.ar(sig, pan, amp);
};

s.sync;

//your paterns:

//drums
Pdefn(\kick,  Pseq([1,0,1,0,1,0, 1, 1, 1,0,1,0,1,0, 1, 0]*1, inf) );
Pdefn(\clap,  Pseq([0,1,0,1]*0.15, inf) );
Pdefn(\bass,  Pseq((0..7).scramble, inf) );
Pdefn(\bassamp,  Pseq([1,0,1,0,1,1, 0, 1, 1,0,1,1,1,0, 1, 1]*0.2, inf) );

Pdef(\drums, Ppar([
            Pbind(\instrument, \eightoeightkick, \freq, 50.midicps, \dur, 0.25, \amp, Pdefn(\kick) ),
            Pbind(\instrument, \handclapsmash, \dur, 0.25, \amp, Pdefn(\clap)),
            Pbind(\instrument, \bassfoundation, \degree, Pdefn(\bass), \dur, 0.25, \amp, Pdefn(\bassamp), \scale, Scale.mixolydian, \octave, 2, \ctranspose, 0)
        ] );
);

//pad chords, melody
Pdefn(\chord1, Pseq([ [0,2,4], [-1,1,3] ], inf) );
Pdefn(\chord2, Pseq([ [3,7], [-1,1,3] ], inf) );
Pdefn(\prophet, Pdefn(\bass) );

Pdef(\pads, Ppar([
            Pbind(\instrument, \plastickystrings, \dur, 1, \amp, Pdefn(\clap)*1.25, \degree, Pdefn(\chord1), \scale, Scale.mixolydian, \stretch, 2, \pan, -0.8 ),
            Pbind(\instrument, \plastickystrings, \dur, 1, \amp, Pdefn(\kick)*0.15, \degree, Pdefn(\chord2), \scale, Scale.mixolydian, \stretch, 2, \octave, 6, \pan, 0.8),
            Pbind(\instrument, \prophet5pwmstrings, \degree, Pdefn(\prophet), \dur, 1, \amp, 0.2, \scale, Scale.mixolydian, \ctranspose, 0)
        ] );
);

Ndef.ar(\master, 2); //stereo ndef for master

~chans = [\chan1, \chan2, \chan3, \chan4, \chan5]; //add more channels here if necessary
~chans.do{|item| Ndef.ar(item,2) };
~ndefs = ~chans.collect{|item| Ndef(item) };

Ndef(\master, {var sig;
    sig = ~ndefs.sum;
    sig = Compander.ar(sig, sig, 0.9, 1, 30, 0.001, 0.01).softclip //limiter
});


Ndef(\chan1, {arg amp=1;
    var sig;
    sig = Ndef.ar(\sound1); //key, numChans
    //your effects
/*    sig = HPF.ar(sig, 440);
    sig = FreeVerb.ar(sig);*/

    sig = sig * amp;
});

Ndef(\chan2, {arg amp=1;
    var sig;
    sig = Ndef.ar(\sound2); //key, numChans
    //your effects
/*    sig = HPF.ar(sig, 440);
    sig = FreeVerb.ar(sig);*/

    sig = sig * amp;
});

Ndef(\chan3, {arg amp=1;
    var sig;
    sig = Ndef.ar(\sound3); //key, numChans
    //your effects
/*    sig = HPF.ar(sig, 440);
    sig = FreeVerb.ar(sig);*/

    sig = sig * amp;
});

Ndef(\chan4, {arg amp=1;
    var sig;
    sig = Ndef.ar(\sound4); //key, numChans
    //your effects
/*    sig = HPF.ar(sig, 440);
    sig = FreeVerb.ar(sig);*/

    sig = sig * amp;
});

Ndef(\chan5, {arg amp=1;
    var sig;
    sig = Ndef.ar(\sound5); //key, numChans
    //your effects
/*    sig = HPF.ar(sig, 440);
    sig = FreeVerb.ar(sig);*/

    sig = sig * amp;
});

//add more channels here if necessary

s.sync;

Ndef(\master).play;
Ndef.all[\localhost].fadeTime = 1; //set all fadeTimes in the server to 1

s.sync; //wait until Ndefs are playing

//your interfacing

//initiate midi
MIDIIn.connectAll;
//or HID
/*~myhid = HID.open( 9571, 1397 );*/

s.sync;

//midi cc

MIDIdef.cc(\slider1, {arg ...args; "chan1: ".post;
                Ndef(\chan1).set(\amp,
                /*args.postln;*/
                args[0].linlin(0,127,0,1.0).postln  );
    }, 1); //maps to cc number 1
MIDIdef.cc(\slider2, {arg ...args; "chan2: ".post;
                Ndef(\chan2).set(\amp, args[0].linlin(0,127,0,1.0).postln );
    }, 2); //maps to cc number 2
MIDIdef.cc(\slider3, {arg ...args; "chan3: ".post;
         Ndef(\chan3).set(\amp,
                args[0].linlin(0,127,0,1.0).postln  );
    }, 3); //maps to cc number 3
MIDIdef.cc(\slider4, {arg ...args; "chan4: ".post;
        Ndef(\chan4).set(\amp,
                args[0].linlin(0,127,0,1.0).postln  );
    }, 4); //maps to cc number 4
MIDIdef.cc(\slider5, {arg ...args; "chan5: ".post;
                Ndef(\chan5).set(\amp,
            args[0].linlin(0,127,0,1.0).postln  );
    }, 5); //maps to cc number 5


//midi note
s.sync;
//a sound to play on the midi keyboard
Ndef(\sound5, {|freq = 440 bend = 0 amp = 0 gate=1
        lforate = 4.85 lfowidth= 0.5 cutoff= 2000 rq=0.5 pan = 0.0|
        var pulse, filter, env;
        pulse = Mix.ar(Pulse.ar((freq*bend.lag(0.1).midiratio)
            *[1,33.5.midiratio],[0.2,0.1],[0.7,0.3]));
        env = Env.adsr(0.0,1.0,0.8,3.0).kr(2,gate);
        filter = BLowPass4.ar(pulse,(cutoff)+200+(freq),rq);
        Pan2.ar(Mix.ar(filter)*env*amp,pan);
 });

//monophonic
MIDIdef.noteOn(\keysOn, {arg ...args; args.postln;
            Ndef(\sound5).spawn(extraArgs: [\freq, args[1].midicps, \gate, 1, \amp, args[0].linlin(0,127,0,1)*0.7, \pan, args[1].linlin(0,127,-1,1)])
        });

MIDIdef.noteOff(\keysOff, {arg ...args; args.postln;
                        Ndef(\sound5).set(\gate, 0);
        });

//polyphonic

/*MIDIdef.noteOn(\keysOn, {arg ...args;
var     freq, amp, pan;
            args.postln;

            freq = args[1].midicps;
            amp = args[0].linlin(0,127,0,1)*0.5;
            pan = args[1].linlin(0,127,-1,1);

            Ndef(\sound5)[args[1]] = {| bend = 0 gate=1
        lforate = 4.85 lfowidth= 0.5 cutoff= 2000 rq=0.5|
        var pulse, filter, env;
        pulse = Mix.ar(Pulse.ar((freq*bend.lag(0.1).midiratio)
            *[1,33.5.midiratio],[0.2,0.1],[0.7,0.3]));
        env = Env.adsr(0.0,1.0,0.8,3.0).kr(2,gate);
        filter = BLowPass4.ar(pulse,(cutoff)+200+(freq),rq);
        Pan2.ar(Mix.ar(filter)*env*amp,pan);
 };

        });

MIDIdef.noteOff(\keysOff, {arg ...args; args.postln;
            Ndef(\sound5).objects[args[1]].set(\gate, 0)

        });*/


//HID messages
/*HIDdef.element(\try, {|...args| args.postln;});*/

//osc messages
/*OSCdef(\fader1, { arg msg, time, addr, recvPort; msg.postln}, '/1/faderA'); //key, func, path
OSCdef(\fader2, { arg msg, time, addr, recvPort; msg.postln}, '/1/faderB');
OSCdef(\fader3, { arg msg, time, addr, recvPort; msg.postln}, '/1/faderC');
OSCdef(\fader4, { arg msg, time, addr, recvPort; msg.postln}, '/1/faderD');*/

"code loaded".postln;
}.fork;
});

)

//mixer amplitudes
(
Ndef(\chan1).set(\amp, 0.0);
Ndef(\chan2).xset(\amp, 0.8);
Ndef(\chan3).xset(\amp, 0.8);
Ndef(\chan4).xset(\amp, 0.5);
Ndef(\chan5).xset(\amp, 0.5);
)



Ndef(\sound1).set(\bufnum, ~buffers[rrand(0, ~buffers.size-1)].postln, \amp, 0.2);
Ndef(\sound1, ~loopBuffer);

Ndef(\sound1, ~abstractBuffer);
Ndef(\sound1).set(\rate, 50, \duration, 60, \lo, 10.1, \hi, 13.2);
Ndef(\sound1).set(\rate, 5, \duration, 5, \lo, -0.6, \hi, -1.2);

(
Ndef(\sound2).set(\bufnum, ~waveBuffer, \lfofreq, [4, 8, 16].choose, \freq, (Scale.mixolydian.degrees+48).choose.midicps, \amp, 3, \cutoffset, [2,3,4,6,8].choose);
Ndef(\sound2, ~oscSynth);
Tdef(\bass, { inf.do{
Ndef(\sound2).set(\bufnum, ~waveBuffer, \lfofreq, [4, 8, 16].choose, \freq, (Scale.mixolydian.degrees+48).choose.midicps, \amp, 3, \cutoffset, [2,3,4,6,8].choose);
[4,8,2].choose.yield;
}
}).play;
)

Ndef.dictFor(s).do(_.quant = 4.0); //quantize all Ndefs to every bar


//patterns into ndefs
Ndef(\sound3, Pdef(\drums) );
Ndef(\sound4, Pdef(\pads) );

Ndef(\sound4, 0 );
Ndef(\sound1, 0 );

Pdefn(\kick,  Pseq([1,0,1,0,1,0, 1, 1, 1,0,1,0,1,1, 1, 0]*1, inf) );
Pdefn(\clap,  Pseq([0,1,1,0]*0.15, inf) );
Pdefn(\bassamp,  Pseq([1,0,1,0,0,1, 0, 1, 1,0,1,1,1,0, 1, 0]*0.2, inf) );

TempoClock.default.tempo = 0.9;
TempoClock.default.tempo = 0.7;
TempoClock.default.tempo = 1;

(
//4-step sequencer
~c1 = 0;
~c2 = 0;
~c3 = 0;
~c4 = 0;
Pdefn(\clap,  Pseq([~c1,~c2,~c3,~c4]*0.15, inf) );

MIDIdef.cc(\knob1, {arg ...args; "chan1: ".post;
    ~c1 = args[0].linlin(0,127,0,1);
    Pdefn(\clap,  Pseq([~c1,~c2,~c3,~c4]*0.15, inf) );
    }, 16); //maps to cc number 16
MIDIdef.cc(\knob1, {arg ...args; "chan1: ".post;
    ~c2 = args[0].linlin(0,127,0,1);
    Pdefn(\clap,  Pseq([~c1,~c2,~c3,~c4]*0.15, inf) );
    }, 18); //maps to cc number 18
MIDIdef.cc(\knob1, {arg ...args; "chan1: ".post;
    ~c3 = args[0].linlin(0,127,0,1);
    Pdefn(\clap,  Pseq([~c1,~c2,~c3,~c4]*0.15, inf) );
    }, 20); //maps to cc number 20
MIDIdef.cc(\knob1, {arg ...args; "chan1: ".post;
    ~c4 = args[0].linlin(0,127,0,1);
    Pdefn(\clap,  Pseq([~c1,~c2,~c3,~c4]*0.15, inf) );
    }, 22); //maps to cc number 22
)

(
//reverb and distortion on drums
Ndef(\chan3, {arg amp=1;
    var sig;
    sig = Ndef.ar(\sound3); //key, numChans
    //your effects
    sig = (sig * 4).distort;
    sig = FreeVerb.ar(sig, 0.8, 0.8, 0.5);
    sig = sig * amp;
});
)


Ndef.clear(2);</pre>
<div class='doclink'>helpfile source: <a href='file:///Users/federicoreuben/Library/Application Support/SuperCollider/Extensions/SSfMT/HelpSource/SSfMT/Code Examples/Putting Things Together.schelp'>/Users/federicoreuben/Library/Application Support/SuperCollider/Extensions/SSfMT/HelpSource/SSfMT/Code Examples/Putting Things Together.schelp</a><br>link::SSfMT/Code Examples/Putting Things Together::<br>sc version: 3.8.0</div></div></body></html>